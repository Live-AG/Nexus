  
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РаботаСБуферомОбменаКлиентСервер.ДобавитьЭлементыФормыДляHTMLБуфера(ЭтотОбъект, Элементы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтобразитьДоступностьКоличествоПользователей();
	ОтобразитьТекущуюСтраницу("Общее");
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЛицензииПриИзменении(Элемент)

	ПеречислениеТипСерверная = ПредопределенноеЗначение("Перечисление.ТипыЛицензий.Серверная");
	Если Объект.ТипЛицензии = ПеречислениеТипСерверная Тогда
		Объект.КоличествоПользователей = 1;
	КонецЕсли;
	
	ОтобразитьДоступностьКоличествоПользователей();
	
КонецПроцедуры       

&НаКлиенте
Процедура ОтобразитьДоступностьКоличествоПользователей()
	
	ПеречислениеТипСерверная = ПредопределенноеЗначение("Перечисление.ТипыЛицензий.Серверная");
	ЭтоСервернаяЛицензия = (Объект.ТипЛицензии <> ПеречислениеТипСерверная);
	Элементы.КоличествоПользователей.Доступность = ЭтоСервернаяЛицензия;

КонецПроцедуры // ОтобразитьДоступностьКоличествоПользователей()

&НаКлиенте
Процедура КомандаСделатьПинКодАктивным(Команда)
	
	ТекущаяСтрокаПинКодов = Элементы.ТаблицаПинКодов.ТекущиеДанные;
	
	Если ТекущаяСтрокаПинКодов <> Неопределено Тогда
		Если Не ЗначениеЗаполнено(ТекущаяСтрокаПинКодов.ДатаАктивации)
			Или Не ЗначениеЗаполнено(ТекущаяСтрокаПинКодов.ИмяКомпьютера) Тогда
			
			ПоказатьПредупреждение(, "Для указания пин-кода как активного необходимо указать Дату активации и Имя компьютера");
		Иначе
			Объект.ТекущийПинКод = ТекущаяСтрокаПинКодов.ПинКод;
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекущийПинКодНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СкопироватьЗначениеВБуфер(Строка(Объект.ТекущийПинКод));	
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитРегистрационныхДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СкопироватьЗначениеВБуфер(Строка(Объект[Элемент.Имя]));
	
КонецПроцедуры 

&НаКлиенте
Процедура СкопироватьЗначениеВБуфер(Значение)

	РаботаСБуферомОбменаКлиентСервер.СкопироватьВБуферОбмена(Значение, ЭтотОбъект["ПолеHTMLБуфера"]);

КонецПроцедуры // СкопироватьЗначениВБуфер()

&НаКлиенте
Процедура КомандаОбщее(Команда)
	
	ОтобразитьТекущуюСтраницу("Общее");
	
КонецПроцедуры       

&НаКлиенте
Процедура КомандаРегистрационныеДанные(Команда)
	
	ОтобразитьТекущуюСтраницу("РегистрационныеДанные");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПинКоды(Команда)
	
	ОтобразитьТекущуюСтраницу("ПинКоды");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьТекущуюСтраницу(ТекущаяСтраницаФормы)

	Если ТекущаяСтраницаФормы = "Общее" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщее;
	ИначеЕсли ТекущаяСтраницаФормы = "РегистрационныеДанные" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРегистрационныеДанные;
	ИначеЕсли ТекущаяСтраницаФормы = "ПинКоды" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПинКоды;
	КонецЕсли;
	
	Элементы.КомандаОбщее.Доступность					= ТекущаяСтраницаФормы <> "Общее";
	Элементы.КомандаРегистрационныеДанные.Доступность	= ТекущаяСтраницаФормы <> "РегистрационныеДанные";
	Элементы.КомандаПинКоды.Доступность				= ТекущаяСтраницаФормы <> "ПинКоды";

КонецПроцедуры // ОтобразитьТекущийОтбор()







