
 
Процедура СобратьСтатистикуИспользованияИнформационныхБаз() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				|	Кластеры1С.Ссылка КАК Кластер
				|ИЗ
				|	Справочник.Кластеры1С КАК Кластеры1С
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеЗаданияОбъектов КАК ТекущиеЗаданияОбъектов
				|		ПО (ТекущиеЗаданияОбъектов.Объект = Кластеры1С.Ссылка)
				|ГДЕ
				|	ТекущиеЗаданияОбъектов.Объект ЕСТЬ NULL
				|	И Кластеры1С.Активен";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТаблицаСеансов = АдминистрированиеСерверов.ПолучитьТаблицуАктивныхСеансовКластера(Выборка.Кластер);
		ЗаписатьТаблицуВРегистр(ТаблицаСеансов);
	КонецЦикла;                                                                  
	
КонецПроцедуры    

Процедура ЗаписатьТаблицуВРегистр(ТаблицаСеансов)
	
	//#TODO Обработать следующие ИмяПриложения:
	//	1CV8 - идентификатор приложения "1С:Предприятие";
	//	1CV8C - идентификатор приложения тонкого клиента "1С:Предприятие";
	//	AgentStandardCall – стандартный вызов;
	//	AnalyticsSystemClient - клиент системы аналитики;
	//	AnalyticsSystemQuery - служебный вызов системы аналитики;
	//	BackgroundJob - идентификатор сессии обработки заданий;
	//	COMConnection - идентификатор сессии внешнего соединения 1С:Предприятия через COM;
	//	COMConsole - идентификатор административной сессии внешнего соединения;
	//	Debugger - идентификатор сессии отладчика;
	//	DebugQueryTargets - идентификатор приложения для IDE-отладчика по TCP - запрос состава предметов отладки;
	//	Designer - идентификатор приложения Конфигуратор;
	//	HTTPServiceConnection - идентификатор сессии http-сервиса;
	//	ImplAppID - идентификатор приложения для временного сеанса;
	//	JobScheduler - идентификатор сессии планировщика заданий;
	//	MobileClient - идентификатор приложения мобильного клиента "1С:Предприятие";
	//	ODataConnection - идентификатор сессии OData-сервиса;
	//	OpenIDProvider - идентификатор провайдера OpenID;
	//	RAS - идентификатор сервера администрирования;
	//	RemoteDebugger - идентификатор приложения для IDE-отладчика по TCP;
	//	SrvrConsole - идентификатор сессии консоли кластера;
	//	SystemBackgroundJob - идентификатор системного фонового задания;
	//	WebClient - идентификатор приложения web-клиента 1С:Предприятие;
	//	WebServerExtension - идентификатор приложения модуля расширения веб-сервера;
	//	WSConnection - идентификатор сессии web-сервиса.
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТаблицаСеансов.ИдентификаторИнформационнойБазы КАК ИдентификаторИнформационнойБазы,
	               |	ТаблицаСеансов.ИмяПользователя КАК ИмяПользователя,
	               |	ТаблицаСеансов.ИмяПриложения КАК ИмяПриложения,
	               |	ТаблицаСеансов.ВремяНачала КАК ВремяНачала,
	               |	ТаблицаСеансов.СпящийСеанс КАК СпящийСеанс
	               |ПОМЕСТИТЬ ВТ_Сеансы
	               |ИЗ
	               |	&ТаблицаСеансов КАК ТаблицаСеансов
	               |
	               |ИНДЕКСИРОВАТЬ ПО
	               |	ИдентификаторИнформационнойБазы,
	               |	ИмяПользователя
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&ТекущаяДата КАК Период,
	               |	ИнформационныеБазы.Ссылка КАК ИнформационнаяБаза,
	               |	ПользователиИБ.Ссылка КАК ПользовательИБ,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПользователиИБ.Ссылка = ЗНАЧЕНИЕ(Справочник.ПользователиИБ.DefUser)
	               |					ИЛИ ВТ_Сеансы.СпящийСеанс
	               |				ТОГДА 0
	               |			ИНАЧЕ 1
	               |		КОНЕЦ) КАК КоличествоАктивныхСеансов,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_Сеансы.СпящийСеанс = ИСТИНА
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК КоличествоСпящихСеансов,
	               |	СУММА(ВЫБОР
	               |			КОГДА ПользователиИБ.Ссылка = ЗНАЧЕНИЕ(Справочник.ПользователиИБ.DefUser)
	               |				ТОГДА 1
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК КоличествоФоновыхЗаданий
	               |ИЗ
	               |	ВТ_Сеансы КАК ВТ_Сеансы
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПользователиИБ КАК ПользователиИБ
	               |		ПО ВТ_Сеансы.ИмяПользователя = ПользователиИБ.ИмяПользователя
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ИнформационныеБазы КАК ИнформационныеБазы
	               |		ПО ВТ_Сеансы.ИдентификаторИнформационнойБазы = ИнформационныеБазы.ИдентификаторБазы
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ИнформационныеБазы.Ссылка,
	               |	ПользователиИБ.Ссылка";
	
	Запрос.УстановитьПараметр("ТаблицаСеансов",	ТаблицаСеансов);
	Запрос.УстановитьПараметр("ТекущаяДата",	ТекущаяДата);
	
	Результат = Запрос.Выполнить();
	НаборЗаписейСтатистики = РегистрыСведений.СтатистикаИспользованияИнформационныхБаз.СоздатьНаборЗаписей();
	НаборЗаписейСтатистики.Загрузить(Результат.Выгрузить());
	
	НаборЗаписейСтатистики.Записать(Ложь);

КонецПроцедуры


