	 
Процедура ДобавитьЭлементыФормыДляHTMLБуфера(Объект, Элементы) Экспорт
	
	ДобавляемыеРеквизиты	= Новый Массив;
	
	Реквизит_ПолеHTMLБуфера = Новый РеквизитФормы("ПолеHTMLБуфера", 
											Новый ОписаниеТипов("Строка", , , Новый КвалификаторыСтроки())
											,	
											, 
											"Поле буфера обмена");
	
	ДобавляемыеРеквизиты.Добавить(Реквизит_ПолеHTMLБуфера);
	
	Объект.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	НовыйЭлементПолеHTML = Элементы.Добавить("ПолеHTMLБуфера", Тип("ПолеФормы"), Элементы.СтраницаБуфер);
	НовыйЭлементПолеHTML.Вид			= ВидПоляФормы.ПолеHTMLДокумента;
	НовыйЭлементПолеHTML.ПутьКДанным	= "ПолеHTMLБуфера";		
	
КонецПроцедуры

Процедура СкопироватьВБуферОбмена(Значение, ПолеHTMLБуфера) Экспорт
	
	ПолеHTMLБуфера = СтрШаблон("<!DOCTYPE html>
							|<html>
							|	<body onload='copy()'>
							|		<input id='input' type='text'/>
							|		<script>
							|			function copy() {
							|				var text = '%1';
							|				var ua = navigator.userAgent;
							|				if (ua.search(/MSIE/) > 0 || ua.search(/Trident/) > 0) {
							|					window.clipboardData.setData('Text', text);
							|				} else {
							|					var copyText = document.getElementById('input');
							|					copyText.value = text;
							|					copyText.select();
							|					document.execCommand('copy');
							|				}
							|			}
							|		</script>
							|	</body>
							|</html>",
							Значение);   
	
	#Если Клиент Тогда
	ПоказатьОповещениеПользователя("Скопировано в буфер",, Значение, БиблиотекаКартинок.СкопироватьВБуфер);
	#КонецЕсли

КонецПроцедуры // СкопироватьВБуферОбмена()