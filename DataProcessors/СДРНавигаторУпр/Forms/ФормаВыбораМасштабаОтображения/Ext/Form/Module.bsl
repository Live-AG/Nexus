
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СписокЗначений = Элементы.ИмяПользователя.СписокВыбора;
	
	Если ПравоДоступа("АдминистрированиеДанных", Метаданные) Тогда
		Для каждого Элем из ПользователиИнформационнойБазы.ПолучитьПользователей() Цикл
			СписокЗначений.Добавить(Элем.Имя);
		КонецЦикла;
		СписокЗначений.СортироватьПоЗначению();
	Иначе
		СписокЗначений.Добавить(ИмяПользователя());
		Элементы.ИмяПользователя.ТолькоПросмотр = истина;
	КонецЕсли;
	
	ВариантОтображения = "Авто";
	ИмяПользователя = ИмяПользователя();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМасштабОтображения(Команда)
	Если не ПустаяСтрока(ВариантОтображения) Тогда
		ПоказатьВопрос(новый ОписаниеОповещения("УстановитьМасштабОтображенияДалее", ЭтаФорма), "Масштаб отображения форм будет изменен. Продолжить?", РежимДиалогаВопрос.ДаНетОтмена, 20);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьМасштабОтображенияДалее(РезультатВопроса, ДопПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Если УстановитьМасштабОтображенияНаСервере(ВариантОтображения, ИмяПользователя) Тогда
			ПоказатьПредупреждение(,"Масштаб отображения форм изменен.
			|Чтобы изменения вступили в силу надо перезайти в 1С:Предприятие.",20);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьМасштабОтображенияНаСервере(ВариантОтображения, знач ИмяПользователя)
	Если ПустаяСтрока(ИмяПользователя) Тогда
		ИмяПользователя = Неопределено;
	КонецЕсли;
	
	Попытка
		Настройка = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения", "",, ИмяПользователя);

		Если Не ТипЗнч(Настройка) = Тип("НастройкиКлиентскогоПриложения") Тогда
		    Настройка = Новый НастройкиКлиентскогоПриложения;
		КонецЕсли;

		Настройка.ВариантМасштабаФормКлиентскогоПриложения = ВариантМасштабаФормКлиентскогоПриложения[ВариантОтображения];
		ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиКлиентскогоПриложения", "", Настройка,, ИмяПользователя);
		Возврат истина;
	Исключение
		Сообщить(ОписаниеОшибки());
		Возврат ложь;
	КонецПопытки;
КонецФункции
