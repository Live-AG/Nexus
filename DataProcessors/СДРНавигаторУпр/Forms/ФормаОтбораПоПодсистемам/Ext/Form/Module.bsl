
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	_ОтборПоПодсистемам = новый Структура("ЕстьОтбор", ложь);
	_ОтборПоПодсистемам = Параметры.Отбор;
	
	вЗаполнитьДеревоПодсистем();
	
	вУстановитьУсловноеОформление();
КонецПроцедуры

&НаСервере
Процедура вУстановитьУсловноеОформление()
	ЭтаФорма.УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = ЭтаФорма.УсловноеОформление.Элементы.Добавить();
	ЭлементОтбора = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = новый ПолеКомпоновкиДанных("_ДеревоПодсистем.ВключатьВОтбор");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = истина;
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Шрифт", новый Шрифт(Элементы._ДеревоПодсистем.Шрифт,,,истина));
	ЭлементУО.Поля.Элементы.Добавить().Поле = новый ПолеКомпоновкиДанных("_ДеревоПодсистемИмя");
КонецПроцедуры



&НаСервере
Процедура вЗаполнитьДеревоПодсистем()
	пКореньДЗ = _ДеревоПодсистем.ПолучитьЭлементы().Добавить();
	пКореньДЗ.Имя = Метаданные.Имя;
	пКореньДЗ.ПолноеИмя = "<Конфигурация>";
	
	пУзелДЗ = пКореньДЗ.ПолучитьЭлементы().Добавить();
	пУзелДЗ.Имя = "<Не входящие в подсистемы>";
	пУзелДЗ.ПолноеИмя = "<!>";
	Если _ОтборПоПодсистемам.ЕстьОтбор Тогда
		пКореньДЗ.ВключатьВОтбор = истина;
		
		Если _ОтборПоПодсистемам.ОтбиратьНеПривязанные Тогда
			пУзелДЗ.Пометка = 1;
			пУзелДЗ.ВключатьВОтбор = истина;
		КонецЕсли;
	КонецЕсли;
	
	пТипСтрока = новый ОписаниеТипов("Строка");
	
	пТабДанные = новый ТаблицаЗначений;
	пТабДанные.Колонки.Добавить("Имя", пТипСтрока);
	пТабДанные.Колонки.Добавить("Синоним", пТипСтрока);
	пТабДанные.Колонки.Добавить("ПолноеИмя", пТипСтрока);
	пТабДанные.Колонки.Добавить("Раскрывать", новый ОписаниеТипов("Булево"));
	
	вЗаполнитьУзелДЗ(пКореньДЗ, Метаданные, пТабДанные);
	
	Если _ОтборПоПодсистемам.ЕстьОтбор Тогда
		вУстановитьПометкуДляУзла(пКореньДЗ);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура вЗаполнитьУзелДЗ(Знач пУзелДЗ, Знач пОбъектМД, Знач пТабРабочая)
	пТабРабочая.Очистить();
	
	Для каждого Элем из пОбъектМД.Подсистемы Цикл
		пСтр = пТабРабочая.Добавить();
		пСтр.Имя = Элем.Имя;
		пСтр.Синоним = Элем.Представление();
		пСтр.ПолноеИмя = Элем.ПолноеИмя();
		пСтр.Раскрывать = (Элем.Подсистемы.Количество() <> 0);
	КонецЦикла;
	
	пТабРабочая.Сортировать("Имя");
	
	пЭлементы = пУзелДЗ.ПолучитьЭлементы();
	Для каждого Элем из пТабРабочая Цикл
		пСтрДЗ = пЭлементы.Добавить();
		ЗаполнитьЗначенияСвойств(пСтрДЗ, Элем);
		
		Если _ОтборПоПодсистемам.ЕстьОтбор Тогда
			Если _ОтборПоПодсистемам.Пометка.Найти(пСтрДЗ.ПолноеИмя) <> Неопределено Тогда
				пСтрДЗ.Пометка = 1;
			КонецЕсли;
			Если _ОтборПоПодсистемам.Подсистемы.Найти(пСтрДЗ.ПолноеИмя) <> Неопределено Тогда
				пСтрДЗ.ВключатьВОтбор = истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Элем.Раскрывать Тогда
			вЗаполнитьУзелДЗ(пСтрДЗ, Метаданные.НайтиПоПолномуИмени(пСтрДЗ.ПолноеИмя), пТабРабочая.СкопироватьКолонки());
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура вУстановитьПометкуДляУзла(пУзелДЗ)
	пЭлементы = пУзелДЗ.ПолучитьЭлементы();
	Для каждого СтрДЗ из пЭлементы Цикл
		вУстановитьПометкуДляУзла(СтрДЗ);
	КонецЦикла;
	
	Если пЭлементы.Количество() <> 0 Тогда
		пУзелДЗ.Пометка = вЗначениеПометкиЭлементовХ(пЭлементы);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция вЗначениеПометкиЭлементовХ(пЭлементы)
	ЕстьПомеченные = ложь;
	ЕстьНепомеченные = ложь;
	
	Для каждого Элем из пЭлементы Цикл
		Если ЕстьПомеченные и ЕстьНепомеченные Тогда
			Прервать;
		КонецЕсли;
		
		Если Элем.Пометка = 2 Тогда
			ЕстьПомеченные = истина;
			ЕстьНепомеченные = истина;
			Прервать;
		ИначеЕсли Элем.Пометка = 1 Тогда
			ЕстьПомеченные = истина;
		Иначе
			ЕстьНепомеченные = истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьПомеченные Тогда
		Если ЕстьНепомеченные Тогда
			Возврат 2;
		Иначе
			Возврат 1;
		КонецЕсли;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции


&НаКлиенте
Процедура ПриОткрытии(Отказ)
КонецПроцедуры

&НаКлиенте
Процедура _ИсключитьПодсистемуИзОтбора(Команда)
	ТекДанные = Элементы._ДеревоПодсистем.ТекущиеДанные;
	Если ТекДанные.ВключатьВОтбор и Лев(ТекДанные.ПолноеИмя,1) <> "<" Тогда
		ТекДанные.ВключатьВОтбор = ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ДеревоПодсистемПометкаПриИзменении(Элемент)
	ТекДанные = Элементы._ДеревоПодсистем.ТекущиеДанные;
	Если ТекДанные.Пометка = 2 Тогда
		ТекДанные.Пометка = 0;
	КонецЕсли;
	ТекДанные.ВключатьВОтбор = (ТекДанные.Пометка <> 0);
	
	вПометитьДетей(ТекДанные);
	вПометитьРодителей(ТекДанные);
КонецПроцедуры

&НаКлиенте
Процедура вПометитьДетей(Элемент)
	пЭлементы = Элемент.ПолучитьЭлементы();
	
	Для каждого Элем из пЭлементы Цикл
		Элем.Пометка = Элемент.Пометка;
		Элем.ВключатьВОтбор = (Элемент.Пометка <> 0);
		вПометитьДетей(Элем);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура вПометитьРодителей(Элемент)
	пРодитель = Элемент.ПолучитьРодителя();
	Если пРодитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	пЭлементы = пРодитель.ПолучитьЭлементы();
	
	Если пЭлементы.Количество() = 0 Тогда
		пРодитель.Пометка = 0;
	ИначеЕсли Элемент.Пометка = 2 Тогда
		пРодитель.Пометка = 2;
	Иначе
		пРодитель.Пометка = вЗначениеПометкиЭлементов(пЭлементы);
	КонецЕсли;
	
	пРодитель.ВключатьВОтбор = (пРодитель.Пометка <> 0);
	
	вПометитьРодителей(пРодитель);
	
КонецПроцедуры

&НаКлиенте
Функция вЗначениеПометкиЭлементов(пЭлементы)
	ЕстьПомеченные = ложь;
	ЕстьНепомеченные = ложь;
	
	Для каждого Элем из пЭлементы Цикл
		Если ЕстьПомеченные и ЕстьНепомеченные Тогда
			Прервать;
		КонецЕсли;
		
		Если Элем.Пометка = 2 Тогда
			ЕстьПомеченные = истина;
			ЕстьНепомеченные = истина;
			Прервать;
		ИначеЕсли Элем.Пометка = 1 Тогда
			ЕстьПомеченные = истина;
		Иначе
			ЕстьНепомеченные = истина;
		КонецЕсли;
		
		Если не (ЕстьПомеченные и ЕстьНепомеченные) Тогда
			ВложенныеЭлементы = Элем.ПолучитьЭлементы();
			Если ВложенныеЭлементы.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			ЗначениеПометкиВложенныхЭлементов = вЗначениеПометкиЭлементов(ВложенныеЭлементы);
			ЕстьПомеченные = ЕстьПомеченные или ЗначениеПометкиВложенныхЭлементов;
			ЕстьНепомеченные = ЕстьНепомеченные или не ЗначениеПометкиВложенныхЭлементов;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьПомеченные Тогда
		Если ЕстьНепомеченные Тогда
			Возврат 2;
		Иначе
			Возврат 1;
		КонецЕсли;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции


&НаКлиенте
Процедура _УстановитьОтбор(Команда)
	пСтрукОтбор = вСформироватьПараметрыОтбора();
	
	Если не пСтрукОтбор.ЕстьОтбор Тогда
		_ОтключитьОтбор(Неопределено);
		Возврат;
	КонецЕсли;
	
	пСтрук = новый Структура("Действие", "Установить");
	пСтрук.Вставить("Отбор", пСтрукОтбор);
	
	Закрыть(пСтрук);
КонецПроцедуры

&НаКлиенте
Процедура _ОтключитьОтбор(Команда)
	пСтрук = новый Структура("Действие", "Отключить");
	Закрыть(пСтрук);
КонецПроцедуры

&НаКлиенте
Функция вСформироватьПараметрыОтбора()
	пСтрук = новый Структура("ЕстьОтбор, ОтбиратьНеПривязанные", ложь, ложь);
	
	пКореньДЗ = _ДеревоПодсистем.ПолучитьЭлементы()[0];
	
	Если пКореньДЗ.Пометка = 0 Тогда
		Возврат пСтрук;
	КонецЕсли;
	
	пМассивПометка = новый Массив;
	пМассивОтбор = новый Массив;
	
	вСформироватьМассивВыбранныхПодсистем(пКореньДЗ, пМассивПометка, пМассивОтбор);
	
	Если пКореньДЗ.Пометка = 1 и пМассивОтбор.Количество() = пМассивПометка.Количество() Тогда
		Возврат пСтрук;
	КонецЕсли;
	
	пСтрук.ЕстьОтбор = истина;
	пСтрук.ОтбиратьНеПривязанные = (пКореньДЗ.ПолучитьЭлементы()[0].ВключатьВОтбор = истина);
	
	пСтрук.Вставить("Пометка", пМассивПометка);
	пСтрук.Вставить("Подсистемы", пМассивОтбор);
	
	Возврат пСтрук;
КонецФункции

&НаКлиенте
Процедура вСформироватьМассивВыбранныхПодсистем(пУзелДЗ, пМассивПометка, пМассивОтбор)
	Для каждого Элем из пУзелДЗ.ПолучитьЭлементы() Цикл
		Если Элем.ПолноеИмя <> "<!>" Тогда
			Если Элем.Пометка <> 0 Тогда
				пМассивПометка.Добавить(Элем.ПолноеИмя);
				
				Если Элем.ВключатьВОтбор Тогда
					пМассивОтбор.Добавить(Элем.ПолноеИмя);
				КонецЕсли;
				
				вСформироватьМассивВыбранныхПодсистем(Элем, пМассивПометка, пМассивОтбор)
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
