
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("_ПутьКФормам", _ПутьКФормам);
	Параметры.Свойство("_ИмяФайла", _ИмяФайла);
	Параметры.Свойство("_Режим", _Режим);
КонецПроцедуры

&НаКлиенте
Функция вРазобратьПутьКФайлу(ПутьКФайлу)
	пСтрук = новый Структура("Каталог, Имя", "", "");
	
	Если не ПустаяСтрока(ПутьКФайлу) Тогда
		Поз = СтрНайти(ПутьКФайлу, "\", НаправлениеПоиска.СКонца);
		Если Поз <> 0 Тогда
			пСтрук.Каталог = СокрЛП(Лев(ПутьКФайлу, Поз-1));
			пСтрук.Имя = СокрЛП(Сред(ПутьКФайлу, Поз+1));
		Иначе
			пСтрук.Имя = СокрЛП(ПутьКФайлу);
		КонецЕсли;
	КонецЕсли;
	
	Возврат пСтрук;
КонецФункции

&НаКлиенте
Процедура _ПутьКФайлуXMLПриИзменении(Элемент)
	_ПутьКФайлуXML = СокрЛП(_ПутьКФайлуXML);
КонецПроцедуры

&НаКлиенте
Процедура _ПутьКФайлуXMLОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = ложь;
	
	Если не ПустаяСтрока(_ПутьКФайлуXML) Тогда
		ТДок = новый ТекстовыйДокумент;
		ТДок.НачатьЧтение(новый ОписаниеОповещения("вПослеЧтенияФайлаXML", ЭтаФорма, ТДок), _ПутьКФайлуXML, "UTF-8");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура вПослеЧтенияФайлаXML(ТДок = Неопределено) Экспорт
	Если ТипЗнч(ТДок) = Тип("ТекстовыйДокумент") Тогда
		ТДок.Показать(_ПутьКФайлуXML);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура _ПутьКФайлуXMLНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = ложь;
	
	Если ПустаяСтрока(_ПутьКФайлуXML) Тогда
		пСтрук = новый Структура("Каталог, Имя", "", _ИмяФайла);
	Иначе
		пСтрук = вРазобратьПутьКФайлу(_ПутьКФайлуXML);
	КонецЕсли;
	
	пДиалогВыбораФайла = новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	пДиалогВыбораФайла.Каталог = пСтрук.Каталог;
	пДиалогВыбораФайла.ПолноеИмяФайла = пСтрук.Имя;
	пДиалогВыбораФайла.Заголовок  = "Файл данных XML";
	пДиалогВыбораФайла.Фильтр     = "Файлы данных XML (*.xml)|*.xml|Все файлы (*.*)|*.*";
	пДиалогВыбораФайла.Расширение = "xml";
	
	пДиалогВыбораФайла.Показать(новый ОписаниеОповещения("вОбработатьВыборФайла", ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура вОбработатьВыборФайла(ВыбранныеФайлы, ДопПарам = Неопределено) Экспорт
	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив") Тогда
		_ПутьКФайлуXML = ВыбранныеФайлы[0];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ВыгрузитьДанные(Команда)
	Если не _ВыгружатьДанныеБезИспользованияФайла и ПустаяСтрока(_ПутьКФайлуXML) Тогда
		ПоказатьПредупреждение(, "Не задан файл для выгрузки данных!", 10);
		Возврат;
	КонецЕсли;
	
	пСтрук = новый Структура;
	пСтрук.Вставить("Режим", _Режим);
	пСтрук.Вставить("ПутьКФайлуXML", _ПутьКФайлуXML);
	пСтрук.Вставить("ВыгружатьДанныеБезИспользованияФайла", _ВыгружатьДанныеБезИспользованияФайла);
	
	Закрыть(пСтрук);
КонецПроцедуры
