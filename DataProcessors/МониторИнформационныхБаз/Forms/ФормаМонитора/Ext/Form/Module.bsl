
#Область ОписаниеПеременных

&НаКлиенте	 
Перем ТекущийВидОтбора;	

&НаКлиенте	 
Перем СтруктураЭлементов;  

#КонецОбласти


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидОтбора = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("МониторИнформационныхБаз", 
															"ВидОтбора", 
															"Кластеры", 
															, 
															ИмяПользователя());
	//ТекущийВидОтбора = ВидОтбора; 
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	ТекущийПользовательИБ = ?(ЗначениеЗаполнено(ТекущийПользователь.ПользовательИБ), ТекущийПользователь.ПользовательИБ, Неопределено);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ТаблицаЭлементовКластера,	"ТекущийПользователь", ТекущийПользователь, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ТаблицаПроектов,				"ТекущийПользователь", ТекущийПользователь, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ТаблицаИзбранного,			"ТекущийПользователь", ТекущийПользователь, Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ТаблицаИнформационныхБаз,	"ТекущийПользовательИБ", ТекущийПользовательИБ, Истина); 
	
	СформироватьМенюИзбранного();
	
КонецПроцедуры 

#КонецОбласти

//#Область ОбработчикиСобытийЭлементовШапкиФормы
//
//// Код процедур и функций
//
//#КонецОбласти
//
//#Область ОбработчикиСобытийЭлементовТаблицыФормы
//
//
//// Код процедур и функций
//
//#КонецОбласти
//
//#Область ОбработчикиКомандФормы
//
//// Код процедур и функций
//
//#КонецОбласти
//
//#Область СлужебныеПроцедурыИФункции
//
//// Код процедур и функций
//
//#КонецОбласти
//				



&НаСервере
Процедура СформироватьМенюИзбранного()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗакрепленныеЭлементы.ЭлементЗакрепления КАК ЭлементЗакрепления,
	               |	ЗакрепленныеЭлементы.НомерВГруппе КАК НомерВГруппе
	               |ПОМЕСТИТЬ ВТ_ЗакрепленныеЭлементы
	               |ИЗ
	               |	РегистрСведений.ЗакрепленныеЭлементы КАК ЗакрепленныеЭлементы
	               |ГДЕ
	               |	ЗакрепленныеЭлементы.Пользователь = &ТекущийПользователь
	               |	И ЗакрепленныеЭлементы.ЭлементЗакрепления ССЫЛКА Справочник.ГруппыИзбранного
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕСТЬNULL(ВТ_ЗакрепленныеЭлементы.НомерВГруппе, 100) КАК ПолеУпорядочивания,
	               |	ГруппыИзбранного.Ссылка КАК ГруппаИзбранных
	               |ИЗ
	               |	Справочник.ГруппыИзбранного КАК ГруппыИзбранного
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗакрепленныеЭлементы КАК ВТ_ЗакрепленныеЭлементы
	               |		ПО (ВТ_ЗакрепленныеЭлементы.ЭлементЗакрепления = ГруппыИзбранного.Ссылка)
	               |ГДЕ
	               |	ГруппыИзбранного.Пользователь = &ТекущийПользователь
	               |	И НЕ ГруппыИзбранного.Предопределенный
	               |	И НЕ ГруппыИзбранного.ПометкаУдаления
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ПолеУпорядочивания,
	               |	ГруппыИзбранного.Ссылка";
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	СчетчикЭлементов = 1;
	
	СтруктураКомандИИзбранного = Новый Структура; 
	
	Если Результат.Пустой() Тогда
		КнопкаМенюИзбранное = Элементы.Добавить("КомендаИзбранное_Нет" + СчетчикЭлементов, 
											Тип("КнопкаФормы"), 
											Элементы.ТаблицаИнформационныхБазКонтекстноеМенюГруппаИзбранное);	
		КнопкаМенюИзбранное.Заголовок		= "<Нет групп избранного>";
		КнопкаМенюИзбранное.Вид			= ВидКнопкиФормы.КнопкаКоманднойПанели;	
		КнопкаМенюИзбранное.Доступность	= Ложь;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураКомандИИзбранного.Вставить("КомендаИзбранное_" + СчетчикЭлементов, Выборка.ГруппаИзбранных);
		
		КомандаМенюИзбранного = Команды.Добавить("КомендаИзбранное_" + СчетчикЭлементов);
		КомандаМенюИзбранного.Заголовок	= Строка(Выборка.ГруппаИзбранных);
		КомандаМенюИзбранного.Действие		= "ВыборИзбранногоИзбранное";
		
		КнопкаМенюИзбранное = Элементы.Добавить("КомендаИзбранное_" + СчетчикЭлементов, 
											Тип("КнопкаФормы"), 
											Элементы.ТаблицаИнформационныхБазКонтекстноеМенюГруппаИзбранное);
		КнопкаМенюИзбранное.ИмяКоманды	= "КомендаИзбранное_" + СчетчикЭлементов;
		КнопкаМенюИзбранное.Вид			= ВидКнопкиФормы.КнопкаКоманднойПанели;
		СчетчикЭлементов = СчетчикЭлементов + 1; 
		
	КонецЦикла;
	
	СтруктураМенюИзбранного = СтруктураКомандИИзбранного; 
	
	ПолучитьМассивИзбранногоДляБазы();
	
КонецПроцедуры // СформироватьМенюИзбранного()

&НаКлиенте
Процедура ВыборИзбранногоИзбранное(Команда)
	
	Команда.Картинка		= БиблиотекаКартинок.КартинкаОтмена;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтобразитьТекущийОтбор();
	
	//#REF Избавиться от вызова сервера
	ЕстьАктивныеЗадания = ЕстьАктивныеЗаданияДляКластеров();  
	
	СтруктураЭлементов = ПолучитьСтруктуруЭлементовПоРазделам();
	
КонецПроцедуры  

&НаСервере
Процедура ЗаписатьПараметрыФормыНаСервере()

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("МониторИнформационныхБаз", "ВидОтбора", ВидОтбора, , ИмяПользователя());

КонецПроцедуры // ЗаписатьПараметрыФормыНаСервере()

&НаКлиенте
Функция ПолучитьСтруктуруЭлементовПоРазделам()
	
	СоответствеОтборов = Новый Соответствие;

	СтруктураЭлементовФормы = Новый Структура;
	СтруктураЭлементовФормы.Вставить("ИмяТаблицы",		"ТаблицаЭлементовКластера");
	СтруктураЭлементовФормы.Вставить("ИмяПоля",			"ЭлементКластера");
	СтруктураЭлементовФормы.Вставить("ТипЭлементаОтбора","Кластеры1С");
	СтруктураЭлементовФормы.Вставить("КнопкаЗакрепить",	"ТаблицаЭлементовКластераКонтекстноеМенюЗакрепитьТекущийЭлемент");
	СтруктураЭлементовФормы.Вставить("КнопкаВверх", 		"ТаблицаЭлементовКластераКонтекстноеМенюПереместитьЗакрепленноеВверх");
	СтруктураЭлементовФормы.Вставить("КнопкаВниз",		"ТаблицаЭлементовКластераКонтекстноеМенюПереместитьЗакрепленноеВниз");
	
	СоответствеОтборов.Вставить("Кластеры", СтруктураЭлементовФормы);
	
	
	СтруктураЭлементовФормы = Новый Структура;
	СтруктураЭлементовФормы.Вставить("ИмяТаблицы",		"ТаблицаПроектов");
	СтруктураЭлементовФормы.Вставить("ИмяПоля",			"Проект");
	СтруктураЭлементовФормы.Вставить("ТипЭлементаОтбора","Проекты");
	СтруктураЭлементовФормы.Вставить("КнопкаЗакрепить",	"ТаблицаПроектовКонтекстноеМенюЗакрепитьТекущийЭлемент");
	СтруктураЭлементовФормы.Вставить("КнопкаВверх", 		"ТаблицаПроектовКонтекстноеМенюПереместитьЗакрепленноеВверх");
	СтруктураЭлементовФормы.Вставить("КнопкаВниз",		"ТаблицаПроектовКонтекстноеМенюПереместитьЗакрепленноеВниз");
	
	СоответствеОтборов.Вставить("Проекты", СтруктураЭлементовФормы);
	
	
	СтруктураЭлементовФормы = Новый Структура;
	СтруктураЭлементовФормы.Вставить("ИмяТаблицы",		"ТаблицаИзбранного");
	СтруктураЭлементовФормы.Вставить("ИмяПоля",			"ГруппаИзбранных");
	СтруктураЭлементовФормы.Вставить("ТипЭлементаОтбора","ГруппыИзбранного");
	СтруктураЭлементовФормы.Вставить("КнопкаЗакрепить",	"ТаблицаИзбранногоКонтекстноеМенюЗакрепитьТекущийЭлемент");
	СтруктураЭлементовФормы.Вставить("КнопкаВверх", 		"ТаблицаИзбранногоКонтекстноеМенюПереместитьЗакрепленноеВверх");
	СтруктураЭлементовФормы.Вставить("КнопкаВниз",		"ТаблицаИзбранногоКонтекстноеМенюПереместитьЗакрепленноеВниз");
	
	СоответствеОтборов.Вставить("Избранное", СтруктураЭлементовФормы);
	
	Возврат СоответствеОтборов;

КонецФункции // ПолучитьСтруктуруЭлементовПоРазделам()

&НаКлиенте
Процедура УстановитьОтборПоТаблицеБаз()

	Если ТекущийВидОтбора <> ВидОтбора Тогда
		
		Для Каждого ЭлементОтбор Из ТаблицаИнформационныхБаз.Отбор.Элементы Цикл
			Если ЭлементОтбор.Представление = ТекущийВидОтбора Тогда
				ЭлементОтбор.Использование = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ТекущийВидОтбора = ВидОтбора;
	
	//#REF Уменьшить когнитивную сложность
	Если ВидОтбора = "Кластеры" И Элементы.ТаблицаЭлементовКластера.ТекущиеДанные <> Неопределено Тогда      
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ТаблицаИнформационныхБаз, 
																	"Владелец", 
																	Элементы.ТаблицаЭлементовКластера.ТекущиеДанные.ЭлементКластера,
																	,
																	ВидОтбора, 
																	Истина);  
		
																	
		Если Элементы.ТаблицаЭлементовКластера.ТекущиеДанные.ИндексСостояния = 0 Тогда
			Элементы.СтраницыИнформационныхБаз.ТекущаяСтраница = Элементы.СтраницаОперацияОжидания;
		Иначе 
			Элементы.СтраницыИнформационныхБаз.ТекущаяСтраница = Элементы.СтраницаИнформационныеБазы;
		КонецЕсли; 
		
	ИначеЕсли ВидОтбора = "Проекты" И Элементы.ТаблицаПроектов.ТекущиеДанные <> Неопределено Тогда
		
		ТекущийПроект = Элементы.ТаблицаПроектов.ТекущиеДанные.Проект;
		ТекущийПроект = ?(ТекущийПроект = ПредопределенноеЗначение("Справочник.Проекты.НеЗаполнен"), 
										ПредопределенноеЗначение("Справочник.Проекты.ПустаяСсылка"),
										ТекущийПроект);
		
		Элементы.СтраницыИнформационныхБаз.ТекущаяСтраница = Элементы.СтраницаИнформационныеБазы;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ТаблицаИнформационныхБаз, 
																	"Проект", 
																	ТекущийПроект,
																	,
																	ВидОтбора, 
																	Истина);
	ИначеЕсли ВидОтбора = "Избранное" И Элементы.ТаблицаИзбранного.ТекущиеДанные <> Неопределено Тогда
		Элементы.СтраницыИнформационныхБаз.ТекущаяСтраница = Элементы.СтраницаИнформационныеБазы;
		УстановитьОтборПоИзбранному(Элементы.ТаблицаИзбранного.ТекущиеДанные.ГруппаИзбранных, ВидОтбора);
	КонецЕсли;

КонецПроцедуры // УстановитьОтборПоТаблицеБаз()

&НаСервере
Процедура УстановитьОтборПоИзбранному(ГруппаИзбранных, ВидОтбора)
	
	Если Не ЗначениеЗаполнено(ГруппаИзбранных) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ТаблицаИнформационныхБаз, 
																		"Ссылка", 
																		,
																		,
																		ВидОтбора, 
																		Ложь);
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТекущийПользователь",	ТекущийПользователь);
	Запрос.УстановитьПараметр("ГруппаИзбранных", 	ГруппаИзбранных); 
	
	ПользовательИБ = ?(ЗначениеЗаполнено(ТекущийПользователь.ПользовательИБ), ТекущийПользователь.ПользовательИБ, Неопределено);
	
	Запрос.УстановитьПараметр("ПользовательИБ",	ПользовательИБ);
	
	Если ГруппаИзбранных = Справочники.ГруппыИзбранного.МоиБазы Тогда
		ТекстЗапроса = "ВЫБРАТЬ
					|	ИнформационныеБазы.Ссылка КАК ИнформационнаяБаза
					|ИЗ
					|	Справочник.ИнформационныеБазы КАК ИнформационныеБазы
					|ГДЕ
					|	НЕ ИнформационныеБазы.ПометкаУдаления
					|	И ИнформационныеБазы.Ответственный = &ПользовательИБ";
		
	ИначеЕсли ГруппаИзбранных = Справочники.ГруппыИзбранного.ЧастоИспользуемые Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ ПЕРВЫЕ 5
					|	ТаблицаДанных.ИнформационнаяБаза КАК ИнформационнаяБаза,
					|	ТаблицаДанных.КоличествоАктивныхСеансов * ТаблицаДанных.МножительПериодов КАК ПолеУпорядочивания
					|ИЗ
					|	(ВЫБРАТЬ
					|		СтатистикаИспользованияИнформационныхБаз.ИнформационнаяБаза КАК ИнформационнаяБаза,
					|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтатистикаИспользованияИнформационныхБаз.Период) КАК МножительПериодов,
					|		СУММА(СтатистикаИспользованияИнформационныхБаз.КоличествоАктивныхСеансов) КАК КоличествоАктивныхСеансов
					|	ИЗ
					|		РегистрСведений.СтатистикаИспользованияИнформационныхБаз КАК СтатистикаИспользованияИнформационныхБаз
					|	ГДЕ
					|		СтатистикаИспользованияИнформационныхБаз.ПользовательИБ = &ПользовательИБ
					|	
					|	СГРУППИРОВАТЬ ПО
					|		СтатистикаИспользованияИнформационныхБаз.ИнформационнаяБаза) КАК ТаблицаДанных
					|
					|УПОРЯДОЧИТЬ ПО
					|	ПолеУпорядочивания УБЫВ";
		
	ИначеЕсли ГруппаИзбранных = Справочники.ГруппыИзбранного.НеИзбранные Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
					|	ЗначенияИзбранных.ИнформационнаяБаза КАК ИнформационнаяБаза
					|ПОМЕСТИТЬ ВТ_ТаблицаИзбранных
					|ИЗ
					|	РегистрСведений.ЗначенияИзбранных КАК ЗначенияИзбранных
					|ГДЕ
					|	ЗначенияИзбранных.ГруппаИзбранных.Пользователь = &ТекущийПользователь
					|
					|ИНДЕКСИРОВАТЬ ПО
					|	ИнформационнаяБаза
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ РАЗРЕШЕННЫЕ
					|	ИнформационныеБазы.Ссылка КАК ИнформационнаяБаза
					|ИЗ
					|	Справочник.ИнформационныеБазы КАК ИнформационныеБазы
					|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаИзбранных КАК ВТ_ТаблицаИзбранных
					|		ПО ВТ_ТаблицаИзбранных.ИнформационнаяБаза = ИнформационныеБазы.Ссылка
					|ГДЕ
					|	ВТ_ТаблицаИзбранных.ИнформационнаяБаза ЕСТЬ NULL
					|	И НЕ ИнформационныеБазы.ПометкаУдаления";
		
	Иначе
		
		ТекстЗапроса = "ВЫБРАТЬ
					|	ЗначенияИзбранных.ИнформационнаяБаза КАК ИнформационнаяБаза
					|ИЗ
					|	РегистрСведений.ЗначенияИзбранных КАК ЗначенияИзбранных
					|ГДЕ
					|	ЗначенияИзбранных.ГруппаИзбранных = &ГруппаИзбранных";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	МассивБаз = Результат.Выгрузить().ВыгрузитьКолонку("ИнформационнаяБаза");
	
	ЗначениеОтбора = Новый СписокЗначений;
	ЗначениеОтбора.ЗагрузитьЗначения(МассивБаз);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ТаблицаИнформационныхБаз, 
																	"Ссылка", 
																	ЗначениеОтбора,
																	ВидСравненияКомпоновкиДанных.ВСписке,
																	ВидОтбора,
																	Истина); 
	

КонецПроцедуры // УстановитьОтборПоИзбранному()

&НаКлиенте
Процедура УстановитьСвойстваМенюЗакрепить()
	
	ЭлементМеню		= Элементы[СтруктураЭлементов[ВидОтбора].КнопкаЗакрепить];
	Значение			= ПолучитьТекущиеДанныеРаздела("ИндексКартинкиЗакрепить");
	ИндексСортировки	= ПолучитьТекущиеДанныеРаздела("ПолеУпорядочивания");
	
	Если Значение = 1 Тогда
		ЭлементМеню.Заголовок	= "Закрепить элемент";
		ЭлементМеню.Картинка	= БиблиотекаКартинок.Закрепить;
	Иначе
		ЭлементМеню.Заголовок	= "Открепить элемент";
		ЭлементМеню.Картинка	= БиблиотекаКартинок.Открепить;
	КонецЕсли;
	
	ЭлементМеню.Доступность = ИндексСортировки > 0 И ИндексСортировки < 101;

КонецПроцедуры // УстановитьСвойстваМенюЗакрепить()

&НаКлиенте
Функция ЗаполнитьОписаниеЭлемента(ТекущиеДанные)
	
	Кластер = ТекущиеДанные.ЭлементКластера;
	СостояниеКластера = ТекущиеДанные.Состояние;
	
	Если СостояниеКластера <> ПредопределенноеЗначение("Перечисление.СостоянияЭлементовКластера.Активен") Тогда
		Возврат ПолучитьМакетНеактивногоЭлемента();
	КонецЕсли;
	
	Если ТипЗнч(Кластер) = Тип("СправочникСсылка.Кластеры1С") Тогда
		Возврат ЗаполнитьОписаниеКластераНаСервере(Кластер);
	ИначеЕсли ТипЗнч(Кластер) = Тип("СправочникСсылка.ИнформационныеБазы") Тогда	
		
	КонецЕсли;
	
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьМакетНеактивногоЭлемента()

	Возврат Обработки.МониторИнформационныхБаз.ПолучитьМакет("МакетНеактивногоЭлемента");

КонецФункции // ПолучитьМакетНеактивногоЭлемента()

&НаСервере
Функция ЗаполнитьОписаниеКластераНаСервере(Кластер)
	
	Возврат Справочники.Кластеры1С.ПолучитьМакетОписанияКластера(Кластер);
	
КонецФункции

&НаКлиенте
Процедура ПолеОписанияЭлементаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОбработки = Неопределено;
	Если Расшифровка.Свойство("ПараметрыБазы", ПараметрыОбработки) Тогда
		
		ДополнительныеПараметры = Новый Структура; 
		ДополнительныеПараметры.Вставить("ПараметрыБазы", ПараметрыОбработки);
		ДополнительныеПараметры.Вставить("Кластер", Элементы.ТаблицаЭлементовКластера.ТекущиеДанные.ЭлементКластера);
		
		ИнформационнаяБаза = ПолучитьНезарегестрированнуюБазу(ДополнительныеПараметры.Кластер, ПараметрыОбработки["name"]);
		
		ОткрытьФорму("Справочник.ИнформационныеБазы.ФормаОбъекта", Новый Структура("Ключ, ДополнительныеПараметры", ИнформационнаяБаза, ДополнительныеПараметры), ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьНезарегестрированнуюБазу(Кластер, ИмяНаСервере1С)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИнформационныеБазы.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ИнформационныеБазы КАК ИнформационныеБазы
	               |ГДЕ
	               |	ИнформационныеБазы.Владелец = &Владелец
	               |	И ИнформационныеБазы.ИмяНаСервере1С = &ИмяНаСервере1С";
	
	Запрос.УстановитьПараметр("Владелец", Кластер);
	Запрос.УстановитьПараметр("ИмяНаСервере1С", ИмяНаСервере1С);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Справочники.ИнформационныеБазы.ПустаяСсылка();
	КонецЕсли;

КонецФункции // ПолучитьНезарегестрированнуюБазу()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "РегистрацияБазы" Тогда
		//#REF Обновить таблицу баз
	ИначеЕсли ИмяСобытия = "РегистрацияИнформационныхБаз" Тогда  
		Элементы.ТаблицаЭлементовКластера.Обновить();
		ОбработатьДлительныеЗадания(Параметр);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьДлительныеЗадания(Результат)

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НаименованиеФоновогоЗадания", "ОбработкаЗаданияКластера");
	
	Если Результат.Статус <> "Выполняется" Тогда
		ПослеЗавершенияДлительногоЗадания(Результат, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПослеЗавершенияДлительногоЗадания", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, Обработчик, ПараметрыОжидания);

КонецПроцедуры // ОбработатьДлительныеЗадания()

&НаКлиенте
Процедура ПослеЗавершенияДлительногоЗадания(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		Элементы.ТаблицаЭлементовКластера.Обновить();
		Если Элементы.ТаблицаЭлементовКластера.ТекущиеДанные <> Неопределено
			И Элементы.ТаблицаЭлементовКластера.ТекущиеДанные.ИндексСостояния = 0 Тогда 
			Элементы.СтраницыИнформационныхБаз.ТекущаяСтраница = Элементы.СтраницаОперацияОжидания;
		Иначе 
			Элементы.СтраницыИнформационныхБаз.ТекущаяСтраница = Элементы.СтраницаИнформационныеБазы;
		КонецЕсли;
		
		Элементы.ТаблицаИнформационныхБаз.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьТекущиеЗаданияКластеров()

	//#REF Уменьшить передачу данных между клиентом и сервером
	//#REF Запретить повторный запуск обработчикаожидания
	Элементы.ТаблицаЭлементовКластера.Обновить();
	Если Не ЕстьАктивныеЗаданияДляКластеров() Тогда
		ОтключитьОбработчикОжидания("ПроверитьТекущиеЗаданияКластеров"); 
		
		Элементы.ТаблицаИнформационныхБаз.Обновить();
		Если Элементы.ТаблицаЭлементовКластера.ТекущиеДанные.ИндексСостояния = 0 Тогда 
			Элементы.СтраницыИнформационныхБаз.ТекущаяСтраница = Элементы.СтраницаОперацияОжидания;
		Иначе 
			Элементы.СтраницыИнформационныхБаз.ТекущаяСтраница = Элементы.СтраницаИнформационныеБазы;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьАктивныеЗаданияДляКластеров()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				|	ТекущиеЗаданияОбъектов.ИдентификаторЗадания КАК ИдентификаторЗадания,
				|	ТекущиеЗаданияОбъектов.ИсточникЗадания КАК ИсточникЗадания
				|ИЗ
				|	РегистрСведений.ТекущиеЗаданияОбъектов КАК ТекущиеЗаданияОбъектов
				|ГДЕ
				|	ТИПЗНАЧЕНИЯ(ТекущиеЗаданияОбъектов.ИсточникЗадания) = ТИП(Справочник.Кластеры1С)";
	
	Результат = Запрос.Выполнить();
	Возврат Не Результат.Пустой();

КонецФункции // ЕстьАктивныеЗаданияДляКластеров()

&НаКлиенте
Процедура ТаблицаИнформационныхБазВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнформационнаяБаза = Элементы.ТаблицаИнформационныхБаз.ТекущаяСтрока;
	Если Поле.Имя = "ТаблицаИнформационныхБазПолеНастройка" Тогда
				
		ОткрытьФорму("Справочник.ИнформационныеБазы.Форма.ФормаЗапускаПриложения", 
						Новый Структура("ИнформационнаяБаза", ИнформационнаяБаза));
				 
	Иначе 
	    	ОткрытьФорму("Справочник.ИнформационныеБазы.ФормаОбъекта", 
						Новый Структура("Ключ", ИнформационнаяБаза), 
						Элемент, 
						ЭтаФорма);	
		
	КонецЕсли;
	
КонецПроцедуры   

&НаКлиенте
Процедура ПослеВыбораИзМеню(ВыбранныйЭлемент, Параметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранныйЭлемент.Значение = "Запустить" Тогда
		ОткрытьФорму("Справочник.ИнформационныеБазы.Форма.ФормаЗапускаПриложения", 
						Новый Структура("ИнформационнаяБаза", Параметры));
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЭлементовКластераПриАктивизацииЯчейки(Элемент)
	
	УстановитьОтборПоТаблицеБаз();       
	УстановитьСвойстваМенюЗакрепить();
	УстановитьДоступностьКнопокПереместить();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПроектовПриАктивизацииЯчейки(Элемент)
	
	УстановитьОтборПоТаблицеБаз();
	УстановитьСвойстваМенюЗакрепить();
	УстановитьДоступностьКнопокПереместить();							
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИзбранногоПриАктивизацииСтроки(Элемент)

	УстановитьОтборПоТаблицеБаз();
	УстановитьСвойстваМенюЗакрепить();
	УстановитьДоступностьКнопокПереместить();

КонецПроцедуры

&НаКлиенте
Процедура ВидОтбораПриИзменении(Элемент)
	
	ОтобразитьТекущийОтбор();	
	
КонецПроцедуры   

&НаКлиенте
Процедура КомандаКластеры(Команда)
	
	ВидОтбора = "Кластеры";
	ОтобразитьТекущийОтбор();
	ЗаписатьПараметрыФормыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПроекты(Команда)
	
	ВидОтбора = "Проекты";
	ОтобразитьТекущийОтбор();
	ЗаписатьПараметрыФормыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаИзбранное(Команда)
	
	ВидОтбора = "Избранное";
	ОтобразитьТекущийОтбор();
	ЗаписатьПараметрыФормыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьТекущийОтбор()

	Если ВидОтбора = "Кластеры" Тогда
		Элементы.ГруппаСтраницыОтборов.ТекущаяСтраница = Элементы.ГруппаКластеры;
		Элементы.ГруппаКнопкиДобавить.ТекущаяСтраница = Элементы.ГруппаДобавитьКластер;
	ИначеЕсли ВидОтбора = "Проекты" Тогда
		Элементы.ГруппаСтраницыОтборов.ТекущаяСтраница = Элементы.ГруппаПроекты;
		Элементы.ГруппаКнопкиДобавить.ТекущаяСтраница = Элементы.ГруппаДобавитьПроект;
	ИначеЕсли ВидОтбора = "Избранное" Тогда
		Элементы.ГруппаСтраницыОтборов.ТекущаяСтраница = Элементы.ГруппаИзбранное;    
		Элементы.ГруппаКнопкиДобавить.ТекущаяСтраница = Элементы.ГруппаДобавитьИзбранное;
	КонецЕсли;
	
	Элементы.КомандаКластеры.Доступность	= Не ВидОтбора = "Кластеры";
	Элементы.КомандаПроекты.Доступность		= Не ВидОтбора = "Проекты";
	Элементы.КомандаИзбранное.Доступность	= Не ВидОтбора = "Избранное";
	
	УстановитьОтборПоТаблицеБаз();

КонецПроцедуры // ОтобразитьТекущийОтбор()

&НаКлиенте
Процедура ДобавитьКластер(Команда)
	
	ОткрытьФорму("Справочник.Кластеры1С.ФормаОбъекта", , ЭтаФорма, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьБазу(Команда)
	
	СтруктураПараметров = Новый Структура; 
	
	Если Элементы.ГруппаСтраницыОтборов.ТекущаяСтраница = Элементы.ГруппаКластеры 
	    И Элементы.ТаблицаЭлементовКластера.ТекущиеДанные <> Неопределено Тогда
		СтруктураПараметров.Вставить("Владелец", Элементы.ТаблицаЭлементовКластера.ТекущаяСтрока);
	ИначеЕсли Элементы.ГруппаСтраницыОтборов.ТекущаяСтраница = Элементы.ГруппаПроекты
		     И Элементы.ТаблицаПроектов.ТекущиеДанные <> Неопределено Тогда	
		СтруктураПараметров.Вставить("Проект", Элементы.ТаблицаПроектов.ТекущаяСтрока);
	КонецЕсли;
	
 	ОткрытьФорму("Справочник.ИнформационныеБазы.Форма.ФормаНовойБазы", 
					Новый Структура("ЗначенияЗаполнения", СтруктураПараметров), 
					ЭтаФорма, 
					ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗакрепитьКластер(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПроект(Команда)
	
	ОткрытьФорму("Справочник.Проекты.ФормаОбъекта", , ЭтаФорма, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрепитьТекущийЭлемент(Команда)
	
	Если ТекущиеДанныеДоступны() Тогда
		ЗакрепитьЭлементНаСервере(СтруктураЭлементов[ВидОтбора].ТипЭлементаОтбора,
								ПолучитьТекущиеДанныеРаздела());
		ОбновитьТаблицуТекущегоРаздела();   
		УстановитьСвойстваМенюЗакрепить();
	КонецЕсли;
	
КонецПроцедуры     


// --------------- Процедуры рефакторинга -----------------

&НаКлиенте
Функция ТекущиеДанныеДоступны()
	
	ИмяТаблицыТекущегоРаздела = СтруктураЭлементов[ВидОтбора].ИмяТаблицы;
	
	Возврат Элементы[ИмяТаблицыТекущегоРаздела].ТекущиеДанные <> Неопределено;

КонецФункции // ТекущиеДанныеДоступны() 

&НаКлиенте
Процедура ОбновитьТаблицуТекущегоРаздела()

	ИмяТаблицыТекущегоРаздела = СтруктураЭлементов[ВидОтбора].ИмяТаблицы;
	Элементы[ИмяТаблицыТекущегоРаздела].Обновить();

КонецПроцедуры // ОбновитьТаблицуТкущегоРаздела()

&НаКлиенте
Функция ПолучитьТекущиеДанныеРаздела(ИмяПоля = Неопределено)
	
	ИмяТаблицы = СтруктураЭлементов[ВидОтбора].ИмяТаблицы;
	Если ИмяПоля = Неопределено Тогда
		ИмяПоля = СтруктураЭлементов[ВидОтбора].ИмяПоля;
	КонецЕсли;
	
	Возврат Элементы[ИмяТаблицы].ТекущиеДанные[ИмяПоля];

КонецФункции // ПолучитьТекущиеДанныеРаздела()


//------------------------------------------------------

&НаСервереБезКонтекста
Процедура ЗакрепитьЭлементНаСервере(ИмяСправочник, Элемент)
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
					|	ЗакрепленныеЭлементы.НомерВГруппе КАК НомерВГруппе,
					|	ВЫБОР
					|		КОГДА ЗакрепленныеЭлементы.ЭлементЗакрепления = &ВыбранныйЭлемент
					|			ТОГДА ИСТИНА
					|		ИНАЧЕ ЛОЖЬ
					|	КОНЕЦ КАК ЭлементЗакреплен,
					|	ЗакрепленныеЭлементы.ЭлементЗакрепления КАК ЭлементЗакрепления
					|ИЗ
					|	РегистрСведений.ЗакрепленныеЭлементы КАК ЗакрепленныеЭлементы
					|ГДЕ
					|	ЗакрепленныеЭлементы.Пользователь = &ТекущийПользователь
					|	И ЗакрепленныеЭлементы.ЭлементЗакрепления ССЫЛКА Справочник." + ИмяСправочник + "
					|
					|УПОРЯДОЧИТЬ ПО
					|	НомерВГруппе
					|ИТОГИ
					|	МАКСИМУМ(НомерВГруппе),
					|	МАКСИМУМ(ЭлементЗакреплен),
					|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЭлементЗакрепления)
					|ПО
					|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("ТекущийПользователь",	ТекущийПользователь);
	Запрос.УстановитьПараметр("ВыбранныйЭлемент",	Элемент);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	НомерВГруппе = 1;
	
	Если Выборка.Следующий() Тогда
	
		Если Выборка.ЭлементЗакреплен Тогда
			ВыборкаДетальная = Выборка.Выбрать();
			
			Пока ВыборкаДетальная.Следующий() Цикл
				Если ВыборкаДетальная.ЭлементЗакрепления <> Элемент Тогда
					ЗаписьРегистра = РегистрыСведений.ЗакрепленныеЭлементы.СоздатьМенеджерЗаписи();
					ЗаписьРегистра.Пользователь			= ТекущийПользователь;
					ЗаписьРегистра.ЭлементЗакрепления	= ВыборкаДетальная.ЭлементЗакрепления;
					ЗаписьРегистра.НомерВГруппе			= НомерВГруппе;
					ЗаписьРегистра.Записать();  
					
					НомерВГруппе = НомерВГруппе + 1;
				Иначе
					ЗаписьРегистра = РегистрыСведений.ЗакрепленныеЭлементы.СоздатьМенеджерЗаписи();
					ЗаписьРегистра.Пользователь			= ТекущийПользователь;
					ЗаписьРегистра.ЭлементЗакрепления	= Элемент;
					ЗаписьРегистра.Удалить();
				КонецЕсли;
			КонецЦикла;
			
			Возврат;      
			
		Иначе
			НомерВГруппе = Выборка.НомерВГруппе + 1	
		КонецЕсли;
	КонецЕсли;  
	
	ЗаписьРегистра = РегистрыСведений.ЗакрепленныеЭлементы.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Пользователь			= ТекущийПользователь;
	ЗаписьРегистра.ЭлементЗакрепления	= Элемент;
	ЗаписьРегистра.НомерВГруппе			= НомерВГруппе;
	ЗаписьРегистра.Записать();

КонецПроцедуры // ЗакрепитьЭлементНаСервере()


#Область Отладка


&НаКлиенте
Процедура ОбновитьДеревоКластеров(Команда)
	
	ОбновитьТаблицуТекущегоРаздела();
		
КонецПроцедуры


&НаКлиенте
Процедура Тест_ПересоздатьКластер(Команда)
	
	Отладка_УдалитьКластер(Элементы.ТаблицаЭлементовКластера.ТекущаяСтрока);
	ОткрытьФорму("Справочник.Кластеры1С.ФормаОбъекта");
	
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура Отладка_УдалитьКластер(Кластер)

	Кластер.ПолучитьОбъект().Удалить();	

КонецПроцедуры // Отладка_УдалитьКластер()

&НаКлиенте
Процедура Тест_ОбновитьРегистрациюБаз(Команда)
	
	РезультатОперации = НачатьРегистрациюБазКластера(Элементы.ТаблицаЭлементовКластера.ТекущаяСтрока);					
	Элементы.ТаблицаЭлементовКластера.Обновить(); 
	УстановитьОтборПоТаблицеБаз();
	ОбработатьДлительныеЗадания(РезультатОперации);
	
КонецПроцедуры 


&НаСервере
Функция НачатьРегистрациюБазКластера(Кластер)
	
	Результат = Справочники.Кластеры1С.НачатьРегистрациюБазКластера(Кластер);
	
	Возврат Результат;
	
КонецФункции // НачатьРегистрациюБазКластера()



#КонецОбласти



&НаКлиенте

Процедура ЗакрепитьИнформационнуюБазу(Команда)
	

	
КонецПроцедуры

&НаКлиенте
Процедура ПереместитьЗакрепленноеВверх(Команда)
	
	Если ТекущиеДанныеДоступны() Тогда
		ПереместитьЭлементНаСервере(СтруктураЭлементов[ВидОтбора].ТипЭлементаОтбора, 
									ПолучитьТекущиеДанныеРаздела("ПолеУпорядочивания"), 
									"Вверх");
		ОбновитьТаблицуТекущегоРаздела();
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ПереместитьЗакрепленноеВниз(Команда)
	
	Если ТекущиеДанныеДоступны() Тогда
		ПереместитьЭлементНаСервере(СтруктураЭлементов[ВидОтбора].ТипЭлементаОтбора, 
									ПолучитьТекущиеДанныеРаздела("ПолеУпорядочивания"), 
									"Вниз");
		ОбновитьТаблицуТекущегоРаздела();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПереместитьЭлементНаСервере(ИмяСправочник, НомерВГруппе, Направление)
	
	Если НомерВГруппе > 99 Или НомерВГруппе < 1 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Если Направление = "Вверх" Тогда
		НомерВерхнейСтроки	= НомерВГруппе - 1;
		НомерНижнейСтроки		= НомерВГруппе;
	ИначеЕсли Направление = "Вниз" Тогда          
		НомерВерхнейСтроки	= НомерВГруппе;
		НомерНижнейСтроки		= НомерВГруппе + 1;
	Иначе
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрШаблон("ВЫБРАТЬ ПЕРВЫЕ 2
							|	ЗакрепленныеЭлементы.Пользователь КАК Пользователь,
							|	ЗакрепленныеЭлементы.ЭлементЗакрепления КАК ЭлементЗакрепления,
							|	ЗакрепленныеЭлементы.НомерВГруппе КАК НомерВГруппе
							|ИЗ
							|	РегистрСведений.ЗакрепленныеЭлементы КАК ЗакрепленныеЭлементы
							|ГДЕ
							|	ЗакрепленныеЭлементы.Пользователь = &ТекущийПользователь
							|	И ЗакрепленныеЭлементы.ЭлементЗакрепления ССЫЛКА Справочник.%1 
							|	И ЗакрепленныеЭлементы.НомерВГруппе МЕЖДУ %2 И %3
							|УПОРЯДОЧИТЬ ПО
							|	ЗакрепленныеЭлементы.НомерВГруппе УБЫВ", 
							ИмяСправочник, 
							НомерВерхнейСтроки,
							НомерНижнейСтроки);
				
	Запрос.УстановитьПараметр("ТекущийПользователь",	ТекущийПользователь);
	Результат	= Запрос.Выполнить();
	Выборка	= Результат.Выбрать();
	
	Если Выборка.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	Выборка.Следующий(); 
	ЗаписьРегистра = РегистрыСведений.ЗакрепленныеЭлементы.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Пользователь			= Выборка.Пользователь;
	ЗаписьРегистра.ЭлементЗакрепления	= Выборка.ЭлементЗакрепления;
	ЗаписьРегистра.НомерВГруппе			= НомерВерхнейСтроки;
	ЗаписьРегистра.Записать();		                        
	
	Выборка.Следующий(); 
	ЗаписьРегистра = РегистрыСведений.ЗакрепленныеЭлементы.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Пользователь			= Выборка.Пользователь;
	ЗаписьРегистра.ЭлементЗакрепления	= Выборка.ЭлементЗакрепления;
	ЗаписьРегистра.НомерВГруппе			= НомерНижнейСтроки;
	ЗаписьРегистра.Записать();

КонецПроцедуры // ПереместитьЭлементНаСервере()

 

&НаКлиенте
Процедура УстановитьДоступностьКнопокПереместить()
	
	ИмяЭлементаВверх	= СтруктураЭлементов[ВидОтбора].КнопкаВверх;
	ИмяЭлементаВниз	= СтруктураЭлементов[ВидОтбора].КнопкаВниз;
	
	ИндексСортировки = ПолучитьТекущиеДанныеРаздела("ПолеУпорядочивания");
	
	Элементы[ИмяЭлементаВверх].Доступность	= ИндексСортировки < 100 И ИндексСортировки > 0;
	Элементы[ИмяЭлементаВниз].Доступность	= ИндексСортировки < 100 И ИндексСортировки > 0;	

КонецПроцедуры // УстановитьДоступностьКнопокПереместить()


&НаКлиенте
Процедура ДобавитьИзбранное(Команда)
	
    ОткрытьФорму("Справочник.ГруппыИзбранного.ФормаОбъекта", , ЭтаФорма, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИнформационныхБазНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
 	Выполнение = (ВидОтбора = "Избранное");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИзбранногоПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	#Если Не МобильноеПриложениеКлиент Тогда
	СтандартнаяОбработка = Ложь; 
	ЭтоКопирование = (ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Копирование);
	УстановитьИзбранноеНаСервере(ПолучитьТекущиеДанныеРаздела(), Строка, ПараметрыПеретаскивания.Значение, ЭтоКопирование);
	
	УстановитьОтборПоТаблицеБаз();
	#КонецЕсли   

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИзбранногоПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	#Если Не МобильноеПриложениеКлиент Тогда
	Если Строка = ПредопределенноеЗначение("Справочник.ГруппыИзбранного.МоиБазы")
	 Или Строка = ПредопределенноеЗначение("Справочник.ГруппыИзбранного.ЧастоИспользуемые") Тогда
		Возврат;
	КонецЕсли; 
	
	Если Строка = ПолучитьТекущиеДанныеРаздела() Тогда
		Возврат;
	КонецЕсли; 
	
	Если Строка = ПредопределенноеЗначение("Справочник.ГруппыИзбранного.НеИзбранные")
	 Или ПолучитьТекущиеДанныеРаздела() = ПредопределенноеЗначение("Справочник.ГруппыИзбранного.НеИзбранные") Тогда
	 
	 	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Перемещение;
		
	КонецЕсли; 
	
	СтандартнаяОбработка = Ложь;
	#КонецЕсли 

КонецПроцедуры    

&НаСервереБезКонтекста
Процедура УстановитьИзбранноеНаСервере(ГруппаИсточник, ГруппаПриемник, МассивЗначений, Копирование)
	
	НачатьТранзакцию();   
	
	Попытка
		Если ГруппаИсточник = Справочники.ГруппыИзбранного.НеИзбранные Тогда	
			ДобавитьЗначенияВИзбранное(ГруппаПриемник, МассивЗначений);
		ИначеЕсли ГруппаПриемник = Справочники.ГруппыИзбранного.НеИзбранные Тогда
			УдалитьЗначенияИзИзбранного(ГруппаИсточник, МассивЗначений);
		Иначе   
			Если Не Копирование Тогда
				УдалитьЗначенияИзИзбранного(ГруппаИсточник, МассивЗначений);
			КонецЕсли;
			
			ДобавитьЗначенияВИзбранное(ГруппаПриемник, МассивЗначений);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();   
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;

КонецПроцедуры // УстановитьИзбранноеНаСервере()

&НаСервереБезКонтекста
Процедура ДобавитьЗначенияВИзбранное(ГруппаИзбранных, МассивЗначений)
	
	Для Каждого ИнформационнаяБаза Из МассивЗначений Цикл 
		НоваяЗапись = РегистрыСведений.ЗначенияИзбранных.СоздатьМенеджерЗаписи();
		НоваяЗапись.ГруппаИзбранных = ГруппаИзбранных;
        	НоваяЗапись.ИнформационнаяБаза = ИнформационнаяБаза;
		НоваяЗапись.Записать();
	КонецЦикла;
		
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура УдалитьЗначенияИзИзбранного(ГруппаИзбранных, МассивЗначений)
	
	Для Каждого ИнформационнаяБаза Из МассивЗначений Цикл 
		НоваяЗапись = РегистрыСведений.ЗначенияИзбранных.СоздатьМенеджерЗаписи();
		НоваяЗапись.ГруппаИзбранных = ГруппаИзбранных;
        	НоваяЗапись.ИнформационнаяБаза = ИнформационнаяБаза;
		НоваяЗапись.Удалить();
	КонецЦикла;
		
КонецПроцедуры


&НаКлиенте
Процедура ЗапуститьПредприятие(Команда)
	
	ТекущаяСтрокаБазы = Элементы.ТаблицаИнформационныхБаз.ТекущиеДанные;
	Если ТекущаяСтрокаБазы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстВыполнения = Новый Структура("ИнформационнаяБаза", ТекущаяСтрокаБазы.Ссылка);
	
	ПриложениеИсполнения = "1cv8c";
	КаталогПрограммы = РаботаСоСкриптамиКлиент.ПолучитьКаталогПрограммы();

	СоставСкрипта = Новый Массив;
	СоставСкрипта.Добавить(КаталогПрограммы + ПриложениеИсполнения);
	СоставСкрипта.Добавить("ENTERPRISE");
	СоставСкрипта.Добавить("/S""%ibServerName%:%ibServerPort%\%ibName%""");
	СоставСкрипта.Добавить("/AppArch x86_64_prt");
	СоставСкрипта.Добавить("/RunModeManagedApplication");
	
	Скрипт = ПолучитьТекстСкриптаНаСервере(СоставСкрипта, КонтекстВыполнения);

	РаботаСоСкриптамиКлиент.ВыполнитьСкрипт(Скрипт);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ЗапуститьКонфигуратор(Команда)
	
	ТекущаяСтрокаБазы = Элементы.ТаблицаИнформационныхБаз.ТекущиеДанные;
	Если ТекущаяСтрокаБазы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстВыполнения = Новый Структура("ИнформационнаяБаза", ТекущаяСтрокаБазы.Ссылка);
	
	ПриложениеИсполнения = "1cv8";
	КаталогПрограммы = РаботаСоСкриптамиКлиент.ПолучитьКаталогПрограммы();

	СоставСкрипта = Новый Массив;
	СоставСкрипта.Добавить(КаталогПрограммы + ПриложениеИсполнения);
	СоставСкрипта.Добавить("DESIGNER");
	СоставСкрипта.Добавить("/S""%ibServerName%:%ibServerPort%\%ibName%""");
	СоставСкрипта.Добавить("/AppArch x86_64_prt");
	
	Скрипт = ПолучитьТекстСкриптаНаСервере(СоставСкрипта, КонтекстВыполнения);

	РаботаСоСкриптамиКлиент.ВыполнитьСкрипт(Скрипт);

КонецПроцедуры


&НаСервере
Функция ПолучитьТекстСкриптаНаСервере(СоставСкрипта, КонтекстВыполнения)
	
	Возврат РаботаСоСкриптамиСервер.ПолучитьТекстСкрипта(СоставСкрипта, КонтекстВыполнения);

КонецФункции

&НаКлиенте
Процедура ПодедитьсяИБ(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры


&НаКлиенте
Процедура НастройкаЗапускаИБ(Команда)

	ТекущаяСтрокаБазы = Элементы.ТаблицаИнформационныхБаз.ТекущиеДанные;
	Если ТекущаяСтрокаБазы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапуска = ПолучитьПараметрыЗапуска(ТекущаяСтрокаБазы.Ссылка);
	
	СтруктураПараметров = Новый Структура("ПараметрыЗапуска", ПараметрыЗапуска);
	ДополнительныеПараметры = Новый Структура("ИнформационнаяБаза", ТекущаяСтрокаБазы.Ссылка);
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбработкаПолученныхПараметровЗапуска", 
																			ЭтотОбъект, 
																			ДополнительныеПараметры);
	ОткрытьФорму("Справочник.ИнформационныеБазы.Форма.ФормаДополнительныхПараметровЗапуска", 
					СтруктураПараметров, 
					, , , , 
					ОписаниеОповещенияОЗакрытии, 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПолученныхПараметровЗапуска(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ЗаписатьПараметрыЗапуска(Результат, ДополнительныеПараметры.ИнформационнаяБаза);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыЗапуска(ТекущаяБазы)
	
	ПараметрыЗапуска = ТекущаяБазы.ПараметрыЗапуска.Получить();
	
	Возврат ПараметрыЗапуска;
		
КонецФункции

&НаСервере
Функция ЗаписатьПараметрыЗапуска(Параметры, ИнформационнаяБаза)
	
	БазаОбъект = ИнформационнаяБаза.ПолучитьОбъект();
	БазаОбъект.ПараметрыЗапуска = Новый ХранилищеЗначения(Параметры);
	БазаОбъект.Записать();
	
КонецФункции

&НаКлиенте
Процедура ТаблицаИнформационныхБазПриАктивизацииСтроки(Элемент)

	Если Элементы.ТаблицаИнформационныхБаз.ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьОтображениеМенюИзбранное(Элементы.ТаблицаИнформационныхБаз.ТекущиеДанные.Ссылка);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОтображениеМенюИзбранное(ИнформационнаяБаза)

	МассивСтрокаИзбранного = ТаблицаСоответствияИзбранного.НайтиСтроки(Новый Структура("ИнформационнаяБаза", ИнформационнаяБаза));
	
	Для Каждого ЭлементСтруктуры Из СтруктураМенюИзбранного Цикл
		
		КартинкаЭлемента = БиблиотекаКартинок.Пустая;
		
		Для Каждого ЭлементМассива Из МассивСтрокаИзбранного Цикл
			
			Если ЭлементМассива.ГруппаИзбранных = ЭлементСтруктуры.Значение Тогда
				КартинкаЭлемента = БиблиотекаКартинок.КартинкаОк;
			Иначе
				КартинкаЭлемента = БиблиотекаКартинок.Пустая;
			КонецЕсли;	
			
		КонецЦикла;
		
		Элементы[ЭлементСтруктуры.Ключ].Картинка	 = КартинкаЭлемента; 
		
	КонецЦикла;

КонецПроцедуры // ЗаполнитьОтображениеМенюИзбранное()   

&НаСервере
Функция ПолучитьМассивИзбранногоДляБазы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗначенияИзбранных.ГруппаИзбранных КАК ГруппаИзбранных,
	               |	ЗначенияИзбранных.ИнформационнаяБаза КАК ИнформационнаяБаза
	               |ИЗ
	               |	РегистрСведений.ЗначенияИзбранных КАК ЗначенияИзбранных
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыИзбранного КАК ГруппыИзбранного
	               |		ПО ЗначенияИзбранных.ГруппаИзбранных = ГруппыИзбранного.Ссылка
	               |			И (ГруппыИзбранного.Пользователь = &ТекущийПользователь)";
	
	Запрос.УстановитьПараметр("ТекущийПользователь", Пользователи.ТекущийПользователь());

	Результат = Запрос.Выполнить();
		
	ТаблицаСоответствияИзбранного.Загрузить(Результат.Выгрузить());

КонецФункции // ПолучитьМассивИзбранногоДляБазы()


&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ТипЗнч(НовыйОбъект) = Тип("СправочникСсылка.ГруппыИзбранного") Тогда
		
		ОбработатьДобавлениеГруппыИзбранного(НовыйОбъект);	
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьДобавлениеГруппыИзбранного(НоваяГруппа)

		

КонецПроцедуры // ОбработатьДобавлениеГруппыИзбранного()



