
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнформационнаяБаза = Параметры.ИнформационнаяБаза;	
	СтрокаПодключения = СтрШаблон("Srvr=""%1"";Ref=""%2""", ИнформационнаяБаза.Владелец, ИнформационнаяБаза.ИмяВКластере1С);  
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВидимостьНастроек();
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеНастройки(Команда)
	
	ПоказыватьНастройки = Не ПоказыватьНастройки;
	
	ВидимостьНастроек();
	
КонецПроцедуры        

&НаКлиенте
Процедура ВидимостьНастроек()
	
	Если ПоказыватьНастройки Тогда
		
		Если ЗапускОтИмениАдминистратора Тогда
			ЗапускОтИмениАдминистратора = Ложь; 
			ВариантАутентификации = 2;
		КонецЕсли;
		
		Элементы.ЗапускОтИмениАдминистратора.Видимость = Ложь;
		Элементы.ГруппаНастройки.Видимость = Истина;
		Элементы.ГруппаКнопок.Видимость = Ложь;
	Иначе
		Элементы.ЗапускОтИмениАдминистратора.Видимость = Истина;	
		Элементы.ГруппаНастройки.Видимость = Ложь;
		Элементы.ГруппаКнопок.Видимость = Истина;
	КонецЕсли;		
	
КонецПроцедуры // ВидимостьНастроек()

&НаКлиенте
Процедура КомандаЗапуститьПредприятиеТонкий(Команда)
	
	ПараметрыЗапуска = Новый Структура;	
	ПараметрыЗапуска.Вставить("Приложение", "ENTERPRISE");
	ПараметрыЗапуска.Вставить("РежимЗапуска", "/AppAutoCheckMode");
	
	ЗапуститьПриложениеПоПараметрам(ПараметрыЗапуска);
	
КонецПроцедуры   

&НаКлиенте
Процедура КомандаЗапуститьПредприятие(Команда)
	
	ПараметрыЗапуска = Новый Структура;	
	ПараметрыЗапуска.Вставить("Приложение", "ENTERPRISE");
	ПараметрыЗапуска.Вставить("РежимЗапуска", "/RunModeManagedApplication");
	
	ЗапуститьПриложениеПоПараметрам(ПараметрыЗапуска);
	
КонецПроцедуры 

&НаКлиенте
Процедура КомандаЗапуститьКонфигуратор(Команда)
	
	ПараметрыЗапуска = Новый Структура;	
	ПараметрыЗапуска.Вставить("Приложение", "DESIGNER");
	ПараметрыЗапуска.Вставить("РежимЗапуска", "/RunModeOrdinaryApplication");
	
	ЗапуститьПриложениеПоПараметрам(ПараметрыЗапуска);	
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗапуститьПриложениеПоПараметрам(ПараметрыЗапуска)

	//#REF необходимо искать файл "1cestart" автоматически
	//
	//ПутьКФайлуПрограммы = КаталогПрограммы() + "1cv8";
	
	ПараметрыСистемы = Новый СистемнаяИнформация;                    
	
	Если ПараметрыСистемы.ТипПлатформы = ТипПлатформы.Windows_x86 
		Или ПараметрыСистемы.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		ПутьКФайлуПрограммы = "C:\Program Files\1cv8\common\1cestart.exe";
	Иначе
		ПутьКФайлуПрограммы = "/opt/1cv8/common/1cestart";
	КонецЕсли;
	
	ФайлЗапускаПрограммы = Новый Файл(ПутьКФайлуПрограммы);	
	Если Не ФайлЗапускаПрограммы.Существует() Тогда
		ПоказатьПредупреждение(, "Файл ""1cestart"" не найден! Укажите путь вручную.");
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапуска.Вставить("ПутьКФайлуПрограммы", ПутьКФайлуПрограммы);
	ПараметрыЗапуска.Вставить("ЗапускОтИмениАдминистратора", ЗапускОтИмениАдминистратора);
	
	КомандаЗапуска = ПолучитьСтрокуЗапускаПриложения(ИнформационнаяБаза, ПараметрыЗапуска);
	
	ЗапуститьПриложение(КомандаЗапуска);	

КонецПроцедуры // ЗапуститьПриложениеПоПараметрам()

&НаСервереБезКонтекста
Функция ПолучитьСтрокуЗапускаПриложения(ИнформационнаяБаза, ПараметрыЗапуска)
	
	Если ПараметрыЗапуска.ЗапускОтИмениАдминистратора Тогда
		ПараметрыПоУмолчанию = Константы.ОбщиеПараметрыАутентификации.ПолучитьСтруктуруНастроекПоУмолчанию();
		ПодстрокаАвторизации = СтрШаблон("/N%1 /P%2 /WA+", ПараметрыПоУмолчанию.ПользовательИБ, ПараметрыПоУмолчанию.ПарольИБ);
	Иначе	                
		ПодстрокаАвторизации = "";
	КонецЕсли;
	
	//РежимЗапуска = "/AppAutoCheckMode";
	//Если Не ПараметрыЗапуска.Свойство("РежимЗапуска", РежимЗапуска) Тогда
	//	РежимЗапуска = "/AppAutoCheckMode";	
	//КонецЕсли;
	
	Возврат СтрШаблон("%1 %2 /S""%3%4\%5"" %6 %7 /AppArch x86_64_prt", 
					ПараметрыЗапуска.ПутьКФайлуПрограммы,
					ПараметрыЗапуска.Приложение,
					ИнформационнаяБаза.Владелец.АдресКластера,
					Формат(ИнформационнаяБаза.Владелец.ПортКластера, "ЧН=' '; ЧГ=0; ЧФ=:Ч"),
					ИнформационнаяБаза.ИмяВКластере1С, 
					ПодстрокаАвторизации,
					ПараметрыЗапуска.РежимЗапуска);
	
КонецФункции





