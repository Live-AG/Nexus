			  
			  
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
// Раздел "Обработчики событий формы" содержит процедуры-обработчики событий формы: ПриСозданииНаСервере, ПриОткрытии и т.п.

 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ТекущаяСтраницаПараметров) Тогда
		ТекущаяСтраницаПараметров = "ГруппаИнформация";
	КонецЕсли;
	
	//Отладка
	УстановитьОтображениеНезаполненныхЭлементов();	
	
	//#TODO Получение Свойств скриптами
	ЖурналРегистрации = "ClickHouse";
	ОбъемБазы = "4.7 Гб (из 11 Гб)";
	КоличествоАктивныхСеансов = 18;
	Объект.ИспользуетсяПо = ?(ЗначениеЗаполнено(Объект.ИспользуетсяПо), Объект.ИспользуетсяПо, ТекущаяДата());
	
	ОтобразитьГрафикАктивности();
	ОтобразитьЭлементыАвторизации(Истина);
	
	РаботаСБуферомОбменаКлиентСервер.ДобавитьЭлементыФормыДляHTMLБуфера(ЭтотОбъект, Элементы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВыполнитьЗаполнениеСвойствБазы();
	
КонецПроцедуры
 

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы
// Раздел "Обработчики событий элементов шапки формы" содержит процедуры-обработчики элементов, расположенных в основной части формы (все, что не связано с таблицами на форме).
#КонецОбласти


#Область ОбработчикиКомандФормы
// Раздел "Обработчики команд формы" содержит процедуры-обработчики команд формы (имена которых задаются в свойстве Действие команд формы).

&НаКлиенте
Процедура КонфигурацияНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ВерсияКонфигурации) Тогда
		
		ОткрытьФорму("Справочник.ВерсииКонфигураций.ФормаОбъекта", 
					Новый Структура("Ключ", Объект.ВерсияКонфигурации), 
					Элемент, ЭтаФорма, 
					, , , 
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе                                                                                                                 
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ИмяЭлемента", "Конфигурация");
		СтруктураПараметров.Вставить("ИмяРеквизита", "ВерсияКонфигурации");
		ОписаниеОпопвещенияВыбора = Новый ОписаниеОповещения("ОбработкаВыбораЗначенияРеквизита", ЭтотОбъект, СтруктураПараметров);
		
		ПараметрыФормыВыбора = Новый Структура;
		ПараметрыФормыВыбора.Вставить("РежимВыбора", Истина);
		
		ОткрытьФорму("Справочник.ВерсииКонфигураций.ФормаВыбора", 
					ПараметрыФормыВыбора, 
					Элемент, ЭтаФорма, 
					, ,
					ОписаниеОпопвещенияВыбора,
					РежимОткрытияОкнаФормы.БлокироватьОкноВладельца)
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура БлокировкаРегламентныхЗаданийПриИзменении(Элемент)
	
	ОписаниеОбработчикаВопроса = Новый ОписаниеОповещения("ИзменениеБлокировкиРегламентныхЗаданий", ЭтотОбъект, БлокировкаРегламентныхЗаданий); 
	ТекстВопроса = ?(БлокировкаРегламентныхЗаданий = Истина, "Установить блокировку регламентных заданий?", "Снять блокировку регламентных заданий?");
	ПоказатьВопрос(ОписаниеОбработчикаВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры  

&НаКлиенте
Процедура ИзменениеБлокировкиРегламентныхЗаданий(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьБлокировкуРегламентныхЗаданийНаСервере(Объект.Ссылка, БлокировкаРегламентныхЗаданий);
	Иначе
		БлокировкаРегламентныхЗаданий = Не БлокировкаРегламентныхЗаданий;
	КонецЕсли;

КонецПроцедуры // ИзменениеБлокировкиРегламентныхЗаданий()

&НаКлиенте
Процедура ОткрытьФормуАвторизации(Команда)
	
	ОписаниеДействияАвторизации = Новый ОписаниеОповещения("ОбработатьДанныеАвторизации", ЭтотОбъект); 
	ОткрытьФорму("ОбщаяФорма.ФормаАвторизации", 
				Новый Структура("ИмяПользователя, ПарольПользователя", Объект.ИмяПользователяИБ, Объект.ПарольПользователяИБ), 
				ЭтаФорма, 
				ЭтаФорма, 
				, , 
				ОписаниеДействияАвторизации, 
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры 

&НаКлиенте
Процедура ОбработатьДанныеАвторизации(Результат, ДополнительныеПараметры) Экспорт

	Если ЗначениеЗаполнено(Результат) Тогда
		Объект.ИмяПользователяИБ		= Результат.ИмяПользователя;
		Объект.ПарольПользователяИБ	= Результат.ПарольПользователя;
		
		Записать();
		
		ВыполнитьЗаполнениеСвойствБазы();
	КонецЕсли;

КонецПроцедуры // ОбработатьДанныеАвторизации()


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Раздел "Служебные процедуры и функции" имеет такое же предназначение, что и в общих модулях.
#КонецОбласти			  
			  
			  
&НаСервере
Процедура УстановитьОтображениеНезаполненныхЭлементов()
	
	//ОформлениеСсылки = Новый Структура("Шрифт, ЦветТекста", Новый Шрифт(, 11, , , Истина), ЦветаСтиля.ГиперссылкаЦвет);
	//ОформлениеПустойСсылки = Новый Структура("Шрифт, ЦветТекста", Новый Шрифт(, 11, , , Истина), ЦветаСтиля.ЦветПустойГиперссылки);
	
	МассивПроверяемыхРеквизитов = Новый Массив;
	МассивПроверяемыхРеквизитов.Добавить("ВерсияКонфигурации");
	//МассивПроверяемыхРеквизитов.Добавить("ИспользуетсяПо");
	МассивПроверяемыхРеквизитов.Добавить("Назначение");
	МассивПроверяемыхРеквизитов.Добавить("Ответственный");
	МассивПроверяемыхРеквизитов.Добавить("Проект");
	МассивПроверяемыхРеквизитов.Добавить("ВебСервер");
	МассивПроверяемыхРеквизитов.Добавить("ХранилищеКонфигурации");

	Для Каждого ПроверяемогоРеквизита Из МассивПроверяемыхРеквизитов Цикл

		ЗначениеРеквизитаЗаполнено	= ЗначениеЗаполнено(Объект[ПроверяемогоРеквизита]);
		ЭлементПустогоРеквизита		= Элементы["Группа" + ПроверяемогоРеквизита + "Пустая"]; 
		ЭлементРеквизита			= Элементы[ПроверяемогоРеквизита];
		
		ЭлементПустогоРеквизита.Видимость	= Не ЗначениеРеквизитаЗаполнено;
		ЭлементРеквизита.Видимость			= ЗначениеРеквизитаЗаполнено; 
	КонецЦикла;
	
КонецПроцедуры // УстановитьОтображениеНезаполненныхЭлементов()


&НаКлиенте
Процедура ОбработкаВыбораЗначенияРеквизита(Результат, ДополнительныеПараметры) Экспорт

	Если ЗначениеЗаполнено(Результат) Тогда
		Объект[ДополнительныеПараметры.ИмяРеквизита] = Результат;  
		Записать(Новый Структура("ЗаписьВФормеПросмотра"));
		Элементы.ДекорацияКонфигурацияПустая.Видимость = Ложь;
		ЭтотОбъект[ДополнительныеПараметры.ИмяЭлемента] = Новый ФорматированнаяСтрока(Строка(Результат), 
																				Новый Шрифт(, 12, , , Истина), 
																				);   
	КонецЕсли;	
	
КонецПроцедуры // ОбработкаВыбораЗначенияРеквизита()     

&НаСервере
Процедура ОтобразитьГрафикАктивности()
	
	Справочники.ИнформационныеБазы.ПолучитьГрафикПоказателейАктивности(Объект.Ссылка, СводнаяИнформация);

КонецПроцедуры // ОтобразитьГрафикАктивности()

&НаКлиенте
Процедура КомандаРедактировать(Команда)

	//ВыполнитьЗаполнениеСвойствБазы();
	//ОтобразитьГрафикАктивности();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура Прототип_ПолучитьПараметрыКонфигурацииНаСервере(ИнформационнаяБаза)
	
	//#TODO Получить Метаданные Базы
	КаталогФайловБазы = КаталогВременныхФайлов() + Строка(Новый УникальныйИдентификатор()) + ПолучитьРазделительПутиСервера(); 
	
	СоздатьКаталог(КаталогФайловБазы);
	
	
	УдалитьФайлы(КаталогФайловБазы);	
	

КонецПроцедуры // Прототип_ПолучитьПараметрыКонфигурацииНаСервере()

&НаСервере
Процедура ОтобразитьДлительныеОперацииПолученияСвойств(ДлительнаяОперацияАктивна)
	
	МассивРеквизитов = Новый Массив;
	МассивРеквизитов.Добавить("ОбъемБазы");
	МассивРеквизитов.Добавить("КоличествоАктивныхСеансов");
	МассивРеквизитов.Добавить("БлокировкаНачалаСеансов");
	МассивРеквизитов.Добавить("БлокировкаРегламентныхЗаданий");
	
	Для Каждого ЭлементМассива Из МассивРеквизитов Цикл
		Элементы[ЭлементМассива].Видимость = Не ДлительнаяОперацияАктивна;
		Элементы["Группа" + ЭлементМассива + "_ПолучениеДанных"].Видимость = ДлительнаяОперацияАктивна;		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьЭлементыАвторизации(АвторизацияПройдена)

	Элементы.ГруппаАдминистрированиеДекорация.Видимость = Не АвторизацияПройдена;
	//Элементы.ОткрытьФормуАвторизации.Видимость = Не АвторизацияПройдена;

КонецПроцедуры // ОтобразитьПараметрыАвторизации()

&НаСервереБезКонтекста
Процедура УстановитьБлокировкуРегламентныхЗаданийНаСервере(ИнформационнаяБаза, Заблокировать)

	АдминистрированиеИнформационныхБаз.УстановитьБлокировкуРегламентныхЗаданий(ИнформационнаяБаза, Заблокировать);					

КонецПроцедуры // УстановитьБлокировкуРегламентныхЗаданийНаСервере()


#Область ДлительныеОперации

&НаКлиенте
Процедура ВыполнитьЗаполнениеСвойствБазы()
	
	ДлительнаяОперация = НачатьПолучениеСвойствБазы();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатПолученияСвойств", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция НачатьПолучениеСвойствБазы()
	
	ОтобразитьДлительныеОперацииПолученияСвойств(Истина);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.КлючФоновогоЗадания		= УникальныйИдентификатор;
	ПараметрыВыполнения.ОжидатьЗавершение			= 0;
	ПараметрыВыполнения.ЗапуститьВФоне				= Истина;
	
	Результат = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
												"АдминистрированиеИнформационныхБаз.ПолучитьСвойстваИнформационнойБазы",
												Объект.Ссылка);

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьРезультатПолученияСвойств(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		СтруктураСвойств = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(СтруктураСвойств) Тогда
			
			ЗаполнитьЭлементыПолученнымиСвойствами(СтруктураСвойств);
			ОтобразитьЭлементыАвторизации(СтруктураСвойств.АвторизацияПройдена);
			ОтобразитьДлительныеОперацииПолученияСвойств(Ложь);
			
			Если СтруктураСвойств.АвторизацияПройдена = Истина Тогда
				ПроверкаИзмененныхРеквизитов(СтруктураСвойств);
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ОтобразитьДлительныеОперацииПолученияСвойств(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭлементыПолученнымиСвойствами(СтруктураСвойств)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураСвойств);
	Если СтруктураСвойств.Свойство("СтруктураПараметровБлокировки", СтруктураПараметровБлокировки) Тогда
		ОтобразитьВремяОкончанияБлокировки();
	КонецЕсли;

КонецПроцедуры // ЗаполнитьЭлементыПолученнымиСвойствами()

&НаКлиенте
Процедура ОтобразитьВремяОкончанияБлокировки()

	ВремяНачалаБлокировки		= СтруктураПараметровБлокировки.ВремяНачалаБлокировки;
	ВремяОкончанияБлокировки	= СтруктураПараметровБлокировки.ВремяОкончанияБлокировки;
	
	ТекстЗаголовка = "";
	ТекстПодсказки = "";
	
	//"с %1(%2) по %3(%4)"
	//"%1 (%2 - %3)"
	//"до %1(%2)"
	//"<Бесспрочно>"
	
	Если БлокировкаНачалаСеансов Тогда
		
		ТекстПодсказки = СтруктураПараметровБлокировки.СообщениеБлокировки;
		
		ТекстДатаНачалаБлокировки		= Формат(ВремяНачалаБлокировки, "ДФ='dd.MM.yy'");
		ТекстДатаОкончанияБлокировки	= Формат(ВремяОкончанияБлокировки, "ДФ='dd.MM.yy'");
	
		ТекстВремяНачалаБлокировки		= Новый ФорматированнаяСтрока(Формат(ВремяНачалаБлокировки, "ДФ='HH:mm'"), Новый Шрифт(,,Истина));
		ТекстВремяОкончанияБлокировки	= Новый ФорматированнаяСтрока(Формат(ВремяОкончанияБлокировки, "ДФ='HH:mm'"), Новый Шрифт(,,Истина));
		
		//REF  -составление текста в зависимости от указанных и заполненных дат
		
		
		Если ВремяНачалаБлокировки < ТекущаяДата() И ТекущаяДата() < ВремяОкончанияБлокировки Тогда
			
			ТекстЗаголовка = Новый ФорматированнаяСтрока("до "+ ТекстДатаОкончанияБлокировки + " (",
													ТекстВремяОкончанияБлокировки,
													")");
			
		ИначеЕсли ТекущаяДата() < ВремяНачалаБлокировки Тогда
			
			Если НачалоДня(ВремяНачалаБлокировки) <> НачалоДня(ВремяОкончанияБлокировки) Тогда
			
				ТекстЗаголовка = Новый ФорматированнаяСтрока(ТекстДатаНачалаБлокировки + " (",
														ТекстВремяНачалаБлокировки,
														") - "+ ТекстДатаОкончанияБлокировки + " (",
														ТекстВремяОкончанияБлокировки,
														")");
			Иначе
				ТекстЗаголовка = Новый ФорматированнаяСтрока(ТекстДатаНачалаБлокировки + " (",
														ТекстВремяНачалаБлокировки,
														" - ",
														ТекстВремяОкончанияБлокировки,
														")"); // заблокировано по
														
			КонецЕсли;
													
		Иначе
			ТекстЗаголовка = "<Бессрочно>"; // заблокировано с
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ЗаголовокБлокировки.Заголовок = Новый ФорматированнаяСтрока(ТекстЗаголовка, Новый Шрифт(, 10));
	Элементы.ЗаголовокБлокировки.Подсказка = ТекстПодсказки;

КонецПроцедуры // ОтобразитьВремяОкончанияБлокировки()

#КонецОбласти


&НаКлиенте
Процедура ПроверкаИзмененныхРеквизитов(СтруктураСвойств)
	
	ЗаписатьИзменений = Ложь;
	
	Если ЗначениеЗаполнено(СтруктураСвойств.ИмяБазыДанных) И Объект.БазаДанных <> СтруктураСвойств.ИмяБазыДанных Тогда
		Объект.БазаДанных = СтруктураСвойств.ИмяБазыДанных;
		ЗаписатьИзменений = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураСвойств.СерверБазДанных) И Объект.СерверБД <> СтруктураСвойств.СерверБазДанных Тогда
		Объект.СерверБД = СтруктураСвойств.СерверБазДанных;
		ЗаписатьИзменений = Истина;
	КонецЕсли;
	
	Если ЗаписатьИзменений Тогда
		Записать();
	КонецЕсли;

КонецПроцедуры // ПроверкаИзмененныхРеквизитов()

&НаКлиенте
Процедура ДекорацияНазначениеНеЗаполненоНажатие(Элемент)
	
	ВыбратьЗначениеРеквизита(Элемент, "Назначение", "Перечисление.НазначенияИБ.ФормаВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОтветственныйНеЗаполненоНажатие(Элемент)
	
	ВыбратьЗначениеРеквизита(Элемент, "Ответственный", "Справочник.ПользователиИБ.ФормаВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПроектНеЗаполненоНажатие(Элемент)
	
	ВыбратьЗначениеРеквизита(Элемент, "Проект", "Справочник.Проекты.ФормаВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВебСерверНеЗаполненоНажатие(Элемент)
	
	ВыбратьЗначениеРеквизита(Элемент, "ВебСервер", "Справочник.ВебСерверы.ФормаВыбора");
	
КонецПроцедуры 

&НаКлиенте
Процедура ДекорацияХранилищеКонфигурацииНеЗаполненоНажатие(Элемент)
	
	ВыбратьЗначениеРеквизита(Элемент, "ХранилищеКонфигурации", "Справочник.ХранилищаКонфигураций.ФормаВыбора");	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЗначениеРеквизита(Элемент, ИмяРеквизита, ИмяФормы)

	ОписаниеОпопвещенияПриВыборе = Новый ОписаниеОповещения("ОбработатьВыборРеквизита", ЭтотОбъект, ИмяРеквизита);
	ОткрытьФорму(ИмяФормы, 
				Новый Структура("РежимВыбора", Истина), 
				Элемент, 
				Элемент, 
				, 
				, 
				ОписаниеОпопвещенияПриВыборе, 
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры // ВыбратьЗначениеЗеквизита()

&НаКлиенте
Процедура ОбработатьВыборРеквизита(Значение, ИмяРеквизита) Экспорт

	Если Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьЗначениеРеквизита(Значение, ИмяРеквизита);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеРеквизита(Значение, ИмяРеквизита)

	Если Объект[ИмяРеквизита] <> Значение Тогда
		Объект[ИмяРеквизита] = Значение;
		Записать();
	
		ЗначениеРеквизитаЗаполнено = ЗначениеЗаполнено(Значение);
		Элементы["Группа" + ИмяРеквизита + "Пустая"].Видимость = Не ЗначениеРеквизитаЗаполнено; 
		Элементы[ИмяРеквизита].Видимость = ЗначениеРеквизитаЗаполнено;
	КонецЕсли;	

КонецПроцедуры // УстановитьЗначениеРеквизита()

&НаКлиенте
Процедура КомандаИзменитьОтветственного(Команда)
	
	ВыбратьЗначениеРеквизита(Элементы.ДекорацияОтветственныйНеЗаполнено, "Ответственный", "Справочник.ПользователиИБ.ФормаВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаИзменитьПроект(Команда)
	
	ВыбратьЗначениеРеквизита(Элементы.ДекорацияПроектНеЗаполнено, "Проект", "Справочник.Проекты.ФормаВыбора");    
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоАктивныхСеансовНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.ИнформационныеБазы.Форма.ФормаПользователей",
				Новый Структура("Ключ", Объект.Ссылка),
				ЭтотОбъект,
				Истина,
				ЭтотОбъект.Окно,
				"e1cib/command/Справочник.ИнформационныеБазы.Команда.Пользователи");
				
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновить(Команда)
	
	ВыполнитьЗаполнениеСвойствБазы();
	ОтобразитьГрафикАктивности();
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЗаголовок(Команда)
	
    	Элементы.ГруппаПросмотраЗаголовка.Видимость = Ложь;   
	Элементы.ГруппаРедактированияЗаголовка.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокОк(Команда)
	
	Если Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	Элементы.ГруппаПросмотраЗаголовка.Видимость = Истина;   
	Элементы.ГруппаРедактированияЗаголовка.Видимость = Ложь;
		
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокОтмена(Команда)
	
	Если Модифицированность Тогда
		ВернутьЗначениеЗаголовка();		
	КонецЕсли;
	
  	Элементы.ГруппаПросмотраЗаголовка.Видимость = Истина;   
	Элементы.ГруппаРедактированияЗаголовка.Видимость = Ложь;
	
КонецПроцедуры   

&НаСервере
Процедура ВернутьЗначениеЗаголовка()

	Объект.Наименование = Объект.Ссылка.Наименование;
	Объект.Описание = Объект.Ссылка.Описание;			
	
КонецПроцедуры

&НаСервере
Процедура уд_ОтладкаНаСервере()

	СведенияОКонфигурации = Справочники.ИнформационныеБазы.ПолучитьСведенияОКонфигурации(Объект.Ссылка);
	
	Если СведенияОКонфигурации = Неопределено Тогда
		Сообщить("Ошибка!!"); 
		Возврат;
	КонецЕсли;
	
	Конфигурация			= Справочники.Конфигурации.НайтиПоРеквизиту("ИмяКонфигурации", СведенияОКонфигурации.ИмяКонфигурации);
	ВерсияКонфигураций	= Справочники.ВерсииКонфигураций.НайтиПоНаименованию(СведенияОКонфигурации.ВерсияКонфигурации, , , Конфигурация);
	
	ТаблицаWebСервисов = Новый ТаблицаЗначений;
	ТаблицаWebСервисов.Колонки.Добавить("WebСервисы");
	ТаблицаWebСервисов.ЗагрузитьКолонку(СведенияОКонфигурации.WebСервисы, "WebСервисы");
	
	ТаблицаHTTPСервисов = Новый ТаблицаЗначений;
	ТаблицаHTTPСервисов.Колонки.Добавить("HTTPСервисы");
	ТаблицаHTTPСервисов.ЗагрузитьКолонку(СведенияОКонфигурации.HTTPСервисы, "HTTPСервисы");
	
	Объект.WebСервисы.Загрузить(ТаблицаWebСервисов);
	Объект.HTTPСервисы.Загрузить(ТаблицаHTTPСервисов);
	
	Объект.ВерсияКонфигурации = ВерсияКонфигураций;

	Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура уд_Отладка(Команда) 
	
	уд_ОтладкаНаСервере();
	
	Значение = Объект.ВерсияКонфигурации;
	ИмяРеквизита = "ВерсияКонфигурации";
	
	ЗначениеРеквизитаЗаполнено = ЗначениеЗаполнено(Значение);
	Элементы["Группа" + ИмяРеквизита + "Пустая"].Видимость = Не ЗначениеРеквизитаЗаполнено; 
	Элементы[ИмяРеквизита].Видимость = ЗначениеРеквизитаЗаполнено;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКластер1СНажатие(Элемент)
	
	ОткрытьФорму("Справочник.Кластеры1С.ФормаОбъекта", Новый Структура("Ключ", Объект.Владелец));
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСУБДНажатие(Элемент)
	
	ОткрытьФорму("Справочник.СерверыБД.ФормаОбъекта", Новый Структура("Ключ", Объект.СерверБД));
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьЗначениеВБуфер(Значение)

	РаботаСБуферомОбменаКлиентСервер.СкопироватьВБуферОбмена(Значение, ЭтотОбъект["ПолеHTMLБуфера"]);

КонецПроцедуры // СкопироватьЗначениВБуфер()

&НаКлиенте
Процедура ИмяДБНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СкопироватьЗначениеВБуфер(Строка(Объект.ИмяИБ));
	
КонецПроцедуры

&НаКлиенте
Процедура Кластер1СНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СкопироватьЗначениеВБуфер(Строка(Объект.Владелец));
	
КонецПроцедуры

&НаКлиенте
Процедура БазаДанныхНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СкопироватьЗначениеВБуфер(Строка(Объект.БазаДанных));
	
КонецПроцедуры

&НаКлиенте
Процедура СерверБДНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СкопироватьЗначениеВБуфер(Строка(Объект.СерверБД));
	
КонецПроцедуры

&НаКлиенте
Процедура БлокировкаНачалаСеансовПриИзменении(Элемент)
	
	Если БлокировкаНачалаСеансов Тогда
		
		ОткрытьФормуБлокировкиНачалаСеансов(Элемент);
		
	Иначе
		ОписаниеОбработчикаВопроса = Новый ОписаниеОповещения("ОтключитьБлокировкуНачалаСеансов", 
																		ЭтотОбъект, 
																		БлокировкаНачалаСеансов);
		ТекстВопроса = "Снять блокировку регламентных заданий?";
		ПоказатьВопрос(ОписаниеОбработчикаВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры  

&НаКлиенте
Процедура ОткрытьФормуБлокировкиНачалаСеансов(Элемент)

	ОписаниеОбработчикаВопроса = Новый ОписаниеОповещения("УстановитьБлокировкуНачалаСеансов", 
														ЭтотОбъект, 
														Элемент);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", Объект.Ссылка);
	ПараметрыФормы.Вставить("СтруктураПараметровБлокировки", СтруктураПараметровБлокировки);
	
	ОткрытьФорму("Справочник.ИнформационныеБазы.Форма.ФормаБлокировкиНачалаСеансов", 
				ПараметрыФормы, 
				ЭтотОбъект, 
				ЭтотОбъект, 
				, 
				, 
				ОписаниеОбработчикаВопроса, 
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры // ОткрытьФормуБлокировкиНачалаСеансов()


&НаКлиенте
Процедура ОтключитьБлокировкуНачалаСеансов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОтключитьБлокировкуНачалаСеансовНаСервере(Объект.Ссылка, СтруктураПараметровБлокировки);
		ОтобразитьВремяОкончанияБлокировки();
	Иначе
		БлокировкаНачалаСеансов = Не БлокировкаНачалаСеансов;
	КонецЕсли;

КонецПроцедуры // ИзменениеБлокировкиРегламентныхЗаданий()

&НаКлиенте
Процедура УстановитьБлокировкуНачалаСеансов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Если ДополнительныеПараметры = Элементы.БлокировкаНачалаСеансов Тогда
			БлокировкаНачалаСеансов = Не БлокировкаНачалаСеансов;
		КонецЕсли;
	Иначе
		СтруктураПараметровБлокировки = Результат;
		ОтобразитьВремяОкончанияБлокировки();
	КонецЕсли;	
	
КонецПроцедуры 

// Отключает Блокировку начала сеансов для указаннной базы
//
// Параметры:
//  <ИнформационнаяБаза>  - <Справочник.ИнформационныеБазы> - Информационная база
//                 <продолжение описания параметра>
//
&НаСервереБезКонтекста
Процедура ОтключитьБлокировкуНачалаСеансовНаСервере(ИнформационнаяБаза, СтруктураПараметровБлокировки)
	
	Если Не ЗначениеЗаполнено(СтруктураПараметровБлокировки) Тогда
		СтруктураПараметровБлокировки = АдминистрированиеИнформационныхБаз.ПолучитьСтруктуруПараметорвБлокировкиБазы();
	КонецЕсли;
	
	СтруктураПараметровБлокировки.ВключенаБлокировкаНачалаСеансов = Ложь;
	АдминистрированиеИнформационныхБаз.УстановитьБлокировкуНачалаСеансов(ИнформационнаяБаза, СтруктураПараметровБлокировки);

КонецПроцедуры // ОтключитьБлокировкуНачалаСеансовНаСервере()

&НаКлиенте
Процедура ЗаголовокБлокировкиНажатие(Элемент)
	
	ОткрытьФормуБлокировкиНачалаСеансов(Элемент);
	
КонецПроцедуры





