
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПутьКФайлуПрограммы = КаталогПрограммы() + "1cv8";
	ПараметрыСистемы = Новый СистемнаяИнформация;                    
	
	Если ПараметрыСистемы.ТипПлатформы = ТипПлатформы.Windows_x86 
		Или ПараметрыСистемы.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		ПутьКФайлуПрограммы  = ПутьКФайлуПрограммы + ".exe";  		
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Сервер1С",			"Сервер1С");	
	СтруктураРеквизитов.Вставить("ПортКластера",		"Сервер1С.ПортКластера");	
	СтруктураРеквизитов.Вставить("ИмяНаСервере1С",	"ИмяНаСервере1С");
	
	СтруктураРеквизитов = ПолучитьЗначениеРаквизита(ПараметрКоманды, СтруктураРеквизитов); 
	
	ПортКластера = ?(ЗначениеЗаполнено(СтруктураРеквизитов.ПортКластера), 
					":" + Формат(СтруктураРеквизитов.ПортКластера, "ЧГ=0"), "");
	
	КомандаЗапуска = СтрШаблон("%1 ENTERPRISE /S""%2%3\%4"" /AppAutoCheckMode /AppArch x86_64_prt", 
							ПутьКФайлуПрограммы, 
							СтруктураРеквизитов.Сервер1С,
							ПортКластера,
							СтруктураРеквизитов.ИмяНаСервере1С);
	
	ЗапуститьПриложение(КомандаЗапуска);        
	
КонецПроцедуры


&НаСервере
Функция ПолучитьЗначениеРаквизита(Объект, Знач СтруктураРеквизитов)
	
	ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(Объект);
	ТекстПолей = "";
	Для Каждого ЭлементСтруктурыРеквизитов Из СтруктураРеквизитов Цикл
		
		ТекстПолей = ТекстПолей + "," + Символы.ПС + 
					ЭлементСтруктурыРеквизитов.Значение + " КАК " + ЭлементСтруктурыРеквизитов.Ключ;
		
	КонецЦикла;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				|	ДанныеОбъекта.Ссылка КАК Ссылка" + ТекстПолей + "
				|ИЗ                           	
				|	" + ИмяТаблицы + " КАК ДанныеОбъекта
				|ГДЕ
				|	ДанныеОбъекта.Ссылка = &Объект";
	
	Запрос.УстановитьПараметр("Объект", Объект);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, Выборка);				
	
	КонецЕсли;
	
	Возврат СтруктураРеквизитов;

КонецФункции // ПолучитьЗначениеРаквизита()