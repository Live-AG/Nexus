
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрукутараПараметров = Константы.ОбщиеПараметрыАутентификации.ПолучитьСтруктуруНастроекПоУмолчанию();
	ЗаполнитьЗначенияСвойств(Объект, СтрукутараПараметров); 
	
	Если Объект.ПортСервераАдминистрирования = 0 Тогда 
		Объект.ПортСервераАдминистрирования = 1545;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьсяКАгенту(Команда)
	
	Отказ = Ложь;
	ПолучитьДанныеАдминистрированиеСервера(Отказ);
	Если Отказ Тогда 
		ПоказатьПредупреждение(, "Авторизация не выполнена!");
	КонецЕсли;
	
	Если СписокКластеров.Количество() > 1 Тогда
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаВыбораКластера;  
	ИначеЕсли СписокКластеров.Количество() = 1 Тогда
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПараметровКластера;
	Иначе
		ПоказатьПредупреждение(, "Для данного сервера администрирования все кластеры зарегестрированы");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКластер(Команда)
	
	Кластер = Элементы.СписокКластеров.ТекущиеДанные.Значение;	
	
	ЗаполнитьРеквизитыКластера(Объект, Кластер);
	
	Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПараметровКластера;
	
КонецПроцедуры 

&НаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыКластера(Объект, Кластер)

	Объект.Наименование			= ?(Кластер.Порт = 1541, Кластер.ИмяКомпьютера, Кластер.ИмяКомпьютера + ":" + Формат(Кластер.Порт, "ЧГ=0"));
	Объект.АдресКластера			= Кластер.ИмяКомпьютера;
	Объект.ПортКластера			= Кластер.Порт;
	Объект.ИдентификаторКластера	= Кластер.ИдентификаторКластера; 
	Объект.Описание				= Кластер.Имя + " (Порт: " + Формат(Кластер.Порт, "ЧГ=0") + ")";

	ЭлементАдминистрированияКластера = АдминистрированиеСерверов.ПолучитьАдиминистрированиеКластера(Объект);
	
	СерверыКластера = ЭлементАдминистрированияКластера.ПолучитьРабочиеСерверы();
	
	Объект.РабочиеСерверыКластера.Очистить();
	Для Каждого РабочийСервер Из СерверыКластера Цикл
		НоваяСтрокаРабочегоСервера = Объект.РабочиеСерверыКластера.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРабочегоСервера, РабочийСервер);
	КонецЦикла;

КонецПроцедуры // ЗаполнитьРеквизитыКластера()

&НаСервере
Процедура ПолучитьДанныеАдминистрированиеСервера(Отказ)

	АгентАдминистрирования = АдминистрированиеСерверов.ПолучитьАгентаАдминистрированияКластера(Объект, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Объект.ВерсияПлатформы = Справочники.ВерсииПлатформы.НайтиПоНаименованию(АгентАдминистрирования.ПолучитьВерсию());
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;

	Попытка
		МассивКластеров = АгентАдминистрирования.ПолучитьКластеры();
	Исключение
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки());
	КонецПопытки;
	
	СписокКластеров.Очистить();
	Для Каждого ЭлементМассива Из МассивКластеров Цикл
		ЗарезервированныйКластер = Справочники.Кластеры1С.НайтиПоРеквизиту("ИдентификаторКластера", ЭлементМассива.ИдентификаторКластера);
		Если Не ЗначениеЗаполнено(ЗарезервированныйКластер) Тогда   
			ОписаниеКластера = Новый Структура("Имя, ИмяКомпьютера, Порт, ИдентификаторКластера");
			ЗаполнитьЗначенияСвойств(ОписаниеКластера, ЭлементМассива);
			СписокКластеров.Добавить(ОписаниеКластера, ЭлементМассива.Имя, , БиблиотекаКартинок.Сервер);
		КонецЕсли;
	КонецЦикла;   
	
	Если СписокКластеров.Количество() = 1 Тогда   
		Кластер = СписокКластеров[0].Значение;
		ЗаполнитьРеквизитыКластера(Объект, Кластер);
	КонецЕсли;

КонецПроцедуры // ПолучитьМассивКластеров()

&НаКлиенте
Процедура ПодключитьсяККластеру(Команда)
	
	ЗаполнитьСписокБаз();
	Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаИнформационныхБаз;				
	
КонецПроцедуры   

&НаСервере
Процедура ЗаполнитьСписокБаз()
	
	СписокБаз.Очистить();
 	БазыКластера = АдминистрированиеСерверов.ПолучитьБазыКластера(Объект); 
	Для Каждого АдминистрированиеБазы Из БазыКластера Цикл 
		
		СписокБаз.Добавить(АдминистрированиеБазы.ИдентификаторИнформационнойБазы, АдминистрированиеБазы.Имя, , БиблиотекаКартинок.БазаДанных);
		
	КонецЦикла;

КонецПроцедуры // ЗаполнитьСписокБаз()

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьБазы(Команда)
	
	РезультатДлительнойОперации = НачатьРегистрациюБазКластера();
	Оповестить("РегистрацияИнформационныхБаз", РезультатДлительнойОперации);
	Закрыть();
	
КонецПроцедуры  

&НаСервере
Функция НачатьРегистрациюБазКластера()
	
	Записать();
		
	Результат = Справочники.Кластеры1С.НачатьРегистрациюБазКластера(Объект.Ссылка);
	
	Возврат Результат;
	
КонецФункции // НачатьРегистрациюБазКластера()

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	Если Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаВыбораКластера Тогда 
		
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПодключенияАгента;	
		
	ИначеЕсли Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПараметровКластера Тогда  
		
		Если СписокКластеров.Количество() > 1 Тогда
			Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаВыбораКластера;  
		Иначе
			Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПодключенияАгента;
		КонецЕсли;	
		
	ИначеЕсли Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаИнформационныхБаз Тогда	 
		
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПараметровКластера;
		
	КонецЕсли;
	
КонецПроцедуры




