
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтрукутараПараметров = Константы.ОбщиеПараметрыАутентификации.ПолучитьСтруктуруНастроекПоУмолчанию();
	ЗаполнитьЗначенияСвойств(Объект, СтрукутараПараметров); 
	
	Если Объект.ПортСервераАдминистрирования = 0 Тогда 
		Объект.ПортСервераАдминистрирования = 1545;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьсяКАгенту(Команда)
	
	ЗаполнитьСписокКластеров();
	
	Если СписокКластеров.Количество() > 1 Тогда
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаВыбораКластера;  
	ИначеЕсли СписокКластеров.Количество() = 1 Тогда
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПараметровКластера;
	Иначе
		ПоказатьПредупреждение(, "Для данного сервера администрирования все кластеры зарегестрированы");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКластер(Команда)
	
	Кластер = Элементы.СписокКластеров.ТекущиеДанные.Значение;	
	Объект.Наименование			= ?(Кластер.Имя = Кластер.ИмяКомпьютера, Кластер.Имя, Кластер.Имя + " (" + Кластер.ИмяКомпьютера + ")");
	Объект.АдресКластера			= Кластер.ИмяКомпьютера;
	Объект.ПортКластера			= Кластер.Порт;
	Объект.ИдентификаторКластера	= Кластер.ИдентификаторКластера;  
	
	Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПараметровКластера;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСписокКластеров()
	
	МассивКластеров = ПолучитьМассивКластеров();
	
	СписокКластеров.Очистить();
	Для Каждого ЭлементМассива Из МассивКластеров Цикл
		ЗарезервированныйКластер = Справочники.Кластеры1С.НайтиПоРеквизиту("ИдентификаторКластера", ЭлементМассива.ИдентификаторКластера);
		Если Не ЗначениеЗаполнено(ЗарезервированныйКластер) Тогда   
			ОписаниеКластера = Новый Структура("Имя, ИмяКомпьютера, Порт, ИдентификаторКластера");
			ЗаполнитьЗначенияСвойств(ОписаниеКластера, ЭлементМассива);
			СписокКластеров.Добавить(ОписаниеКластера, ЭлементМассива.Имя, , БиблиотекаКартинок.Сервер);
		КонецЕсли;
	КонецЦикла;   
	
	Если СписокКластеров.Количество() = 1 Тогда   
		
		Кластер = СписокКластеров[0].Значение;
		
		Объект.Наименование			= Кластер.Имя;
		Объект.АдресКластера			= Кластер.ИмяКомпьютера;
		Объект.ПортКластера			= Кластер.Порт;
		Объект.ИдентификаторКластера	= Кластер.ИдентификаторКластера;
	КонецЕсли;

КонецПроцедуры // ПолучитьМассивКластеров()


&НаСервере
Функция ПолучитьМассивКластеров()

	АгентАдминистрирования = АдминистрированиеСерверов.ПолучитьАгентаАдминистрированияКластера(Объект);
	
	Возврат АгентАдминистрирования.ПолучитьКластеры();

КонецФункции // ПолучитьМассивКластеров()
  
&НаКлиенте
Процедура ПодключитьсяККластеру(Команда)
	
	ЗаполнитьСписокБаз();
	Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаИнформационныхБаз;				
	
КонецПроцедуры   

&НаСервере
Процедура ЗаполнитьСписокБаз()
	
	СписокБаз.Очистить();
 	БазыКластера = АдминистрированиеСерверов.ПолучитьБазыКластера(Объект); 
	Для Каждого АдминистрированиеБазы Из БазыКластера Цикл
		СписокБаз.Добавить(АдминистрированиеБазы.ИдентификаторИнформационнойБазы, АдминистрированиеБазы.Имя, , БиблиотекаКартинок.БазаДанных);
	КонецЦикла;

КонецПроцедуры // ЗаполнитьСписокБаз()


&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьБазы(Команда)
	
	НачатьРегистрациюБазКластера();
	Оповестить("РегистрацияИнформационныхБаз", Объект.Ссылка);
	Закрыть();
	
КонецПроцедуры  

&НаСервере
Процедура НачатьРегистрациюБазКластера()
	
	Записать();
		
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.КлючФоновогоЗадания = Строка(Объект.Ссылка.УникальныйИдентификатор());
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	Объект.ИдентификаторЗадания = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, "Справочники.Кластеры1С.СоздатьБазыПоДаннымКластера", Объект.Ссылка);
	Записать();
	
КонецПроцедуры // НачатьРегистрациюБазКластера()

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	Если Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаВыбораКластера Тогда 
		
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПодключенияАгента;	
		
	ИначеЕсли Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПараметровКластера Тогда  
		
		Если СписокКластеров.Количество() > 1 Тогда
			Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаВыбораКластера;  
		Иначе
			Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПодключенияАгента;
		КонецЕсли;	
		
	ИначеЕсли Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаИнформационныхБаз Тогда	 
		
		Элементы.СтраницыШагов.ТекущаяСтраница = Элементы.СтраницаПараметровКластера;
		
	КонецЕсли;
	
КонецПроцедуры




